#define DX_MAIN_HANDLES
(
   (dx_main_on_enter
      ;correct bns to new speed system
      {if {modifier_mgr is_modifier_active mod_doublespeed}
         {modifier_mgr toggle_modifier_enabled mod_doublespeed}
         {set $trackspeed_guitar {dx_fixup_bns $trackspeed_guitar}}
         {set $trackspeed_bass {dx_fixup_bns $trackspeed_bass}}
         {set $trackspeed_drum {dx_fixup_bns $trackspeed_drum}}
         {set $trackspeed_keys {dx_fixup_bns $trackspeed_keys}}
         {set $trackspeed_real_keys {dx_fixup_bns $trackspeed_real_keys}}
         {set $trackspeed_real_guitar {dx_fixup_bns $trackspeed_real_guitar}}
         {set $trackspeed_real_bass {dx_fixup_bns $trackspeed_real_bass}}
         {dx_settings_dta_writer}
      }
      ;handles for event mode
      {if $dx_event_mode
         {unless $dx_event_type
            {unless $dx_event_welcome_screen_seen
               {ui push_screen dx_EventModeWelcome_screen}
            }
         }

         ;force set some modifiers
         ;force enable no fail
         {unless {modifier_mgr is_modifier_active mod_no_fail_band}
            {modifier_mgr toggle_modifier_enabled mod_no_fail_band}
         }
         ;force enable AIM
         {unless {modifier_mgr is_modifier_active mod_auto_vocals}
            {modifier_mgr toggle_modifier_enabled mod_auto_vocals}
         }
         ;force DISABLE drum navigation (people like to fuck with the drums and i know from experience)
         ;{if {modifier_mgr is_modifier_active mod_drum_surface_navigation}
         ;   {modifier_mgr toggle_modifier_enabled mod_drum_surface_navigation}
         ;}
         ;force DISABLE drum fills (we have this off by default now so)
         {if {modifier_mgr is_modifier_active mod_drum_fills}
            {modifier_mgr toggle_modifier_enabled mod_drum_fills}
         }
         ;force enable indiv track speeds
         {unless {modifier_mgr is_modifier_active mod_independent_track_speeds}
            {modifier_mgr toggle_modifier_enabled mod_independent_track_speeds}
         }
         ;force enable beginner tutorials
         {unless {modifier_mgr is_modifier_active enable_popup_help}
            {modifier_mgr toggle_modifier_enabled enable_popup_help}
         }

         ;setup the main menu
         {menu_buttons.grp set_local_pos -350 0 -90}
         {if {! {exists event.lbl}}
            {switch $dx_event_type
               (party
                  kDataUnhandled
               )
               (club
                  {$this lbl_maker event.lbl -30 -30 kTopLeft {sprint " "}}
                  {event.lbl set_local_scale 1.25 1.25 1.25}
                  {event.lbl set_token_fmt {sprint {localize dx_event_club_lbl}}}
               )
               (convention
                  {$this lbl_maker event.lbl -30 -17 kTopLeft {sprint " "}}
                  {event.lbl set_local_scale 2 2 2}
                  {event.lbl set_token_fmt {sprint {localize dx_welcome_to} $dx_event_name {localize dx_event_convention_lbl}}}
               )
            }
         }
         {if {! {exists event_button_desc.lbl}}
            {$this lbl_maker event_button_desc.lbl -367 -150 kBottomLeft {sprint " "}}
            {event_button_desc.lbl set_local_scale 1.25 1.25 1.25}
            {event_button_desc.lbl set width 10000}
         }
         {event_button_desc.lbl set_token_fmt {sprint {localize dx_freeplay_desc}}}
         {messages.grp set_showing FALSE}

         ;end event mode setup
      }
      {if_else {== $dx_vsync 0}
         {rnd set_sync 0}
         {rnd set_sync 1}
      }
      {if {&& {! $dx_playing_a_show} {! $dx_prep_show_flow} {! $dx_in_show_flow}}
         {set $dx_play_a_show_active_instruments ()}
         {resize $dx_play_a_show_active_instruments 0}
      }
      {if_else $dx_playing_a_show
         {$this dx_play_a_show}
         {do
            #ifndef HX_WII
            DX_SET_SERVER_LOCALE ; dx - change button and label text when connected to ashcentral
            {dx_presence_manager}
            #endif
            {dx_sv_menu_logo_set sv3_panel}
            {set $dx_random_number {random_int 0 4096}}
            {if {== $dx_random_number 2112}
               {dx_passive_messenger you_lost}
            }
            {$this dx_check_birthday}
            #ifndef HX_PS3
            {content_mgr start_refresh}
            #endif
            {mb_musicstore.btn set markup TRUE}
            {mb_musicstore.btn set alt_style_enabled TRUE}
            {mb_musicstore.btn set alt_font_resource_name {mb_musicstore.btn get resource_name}}
            {unless $dx_event_mode
               {set $localize_num {random_int 0 100}}
               {if {== $localize_num 42}
                  {ui_event_mgr trigger_event error_message
                     (dummy init localize_it)
                  }
               }
            }
         }
      }
   )
   (lbl_maker
      ($label $x $y $align $content)
      {new BandLabel $label}
      {$label set resource_name "pentatonic_outline"}
      {$label set alt_style_enabled TRUE}
      {$label set alt_font_resource_name pentatonic_outline}
      {if {! {exists now_play_color.color}}
         {new UIColor now_play_color.color}
         {now_play_color.color set color {pack_color 1 1 1}}
      }
      {$label set markup TRUE}
      {$label set_showing TRUE}
      {$label set_local_scale 1 1 1}
      {$label set_local_pos $x 0 $y}
      {$label set set_token_fmt $content}
      {$label set text_size 15}
      {$label set alt_text_size 15}
      {$label set alt_z_offset -4}
      {$label set fit_type 2}
      {$label set alignment $align}
      {$label set width 300}
      {$label set height 40}
      {$label set alpha 1}
      ;{$label set color_override now_play_color.color}
      {main_menu.grp add_object $label}
   )
   (dx_now_playing
      {main_menu.grp set_showing TRUE}
      {meta_performer reset_songs}
      {meta_performer set_song {symbol $metashortname}}
      {if {! {exists line0.lbl}}
         {$this lbl_maker line0.lbl 385 145 kTopRight {sprint $currentshellmusic}}
      }
      {if {! {exists line1.lbl}}
         {$this lbl_maker line1.lbl 385 160 kTopRight {sprint $currentshellmusic}}
         {line1.lbl set alt_z_offset -2}
         {line1.lbl set alt_text_size 12}
      }
      {if {! {exists line2.lbl}}
         {$this lbl_maker line2.lbl 385 145 kTopRight {sprint $currentshellmusic}}
      }
      {unless $dx_event_mode
         {if {! {exists line3.lbl}}
            {$this lbl_maker line3.lbl 385 145 kTopRight {sprint "<alt>j</alt>playnow"}}
            {line3.lbl set alt_font_resource_name buttons}
            {line3.lbl set alt_z_offset -1}
            {line3.lbl set alt_text_size 12}
         }
      }
      {line0.lbl set_token_fmt {sprint " "}}
      {line1.lbl set_token_fmt {sprint $metasong}}
      {line2.lbl set_token_fmt {sprint " "}}
      {unless $dx_event_mode
         {if_else {meta_performer song}
            {if {&& {session is_local} {meta_performer song_id}}
               {line3.lbl set_token_fmt press_to_play {sprint $metaverb}}
            }
            {line3.lbl set_token_fmt {sprint " "}}
         }
      }
      {if {exists line1_fade}
         {delete line1_fade}
      }
      {$this dx_now_playing_anim}
   )
   (dx_now_playing_anim
      {thread_task kTaskSeconds
         (name line1_fade)
         (script
            {if {> {line1.lbl get_local_pos_index 2} 180}
               {set $direction down}
               {set $whichone {! $whichone}}
               {if_else $whichone
                  {line1.lbl set_token_fmt {sprint $metaartist}}
                  {line1.lbl set_token_fmt {sprint $metasong}}
               }
            }
            {if {<= {line1.lbl get_local_pos_index 2} 160}
               {set $direction up}
               {$task sleep 5}
            }
            {if {== $direction up}
               {line1.lbl set_local_pos_index 2 {+ {line1.lbl get_local_pos_index 2} 1}}
            }
            {if {== $direction down}
               {line1.lbl set_local_pos_index 2 {- {line1.lbl get_local_pos_index 2} 1}}
            }
            {$task sleep 0.0001}
            {$task loop}
         )
      }
   )
   (dx_unlock_pre_orders
      {switch {{$player get_user} get_slot_num}
         (0
            {main_hub_panel unlock_gamestop_award 0}
            {main_hub_panel unlock_x79_award 0}
            {main_hub_panel unlock_d2010_award 0}
         )
         (1
            {main_hub_panel unlock_gamestop_award 1}
            {main_hub_panel unlock_x79_award 1}
            {main_hub_panel unlock_d2010_award 1}
         )
         (2
            {main_hub_panel unlock_gamestop_award 2}
            {main_hub_panel unlock_x79_award 2}
            {main_hub_panel unlock_d2010_award 2}
         )
         (3
            {main_hub_panel unlock_gamestop_award 3}
            {main_hub_panel unlock_x79_award 3}
            {main_hub_panel unlock_d2010_award 3}
         )
      }
   )
   (dx_check_dx
      {unless $dx_event_mode
         {unless {file_exists DX_SETTINGS_FILE_PATH}
            {unless $dxwelcomeseen
               {profile_mgr set_voice_chat_volume 0}
               {dx_log_writer info {sprint "Cannot find DX.dta"}}
               {ui push_screen hint_dx_welcome_screen}
            }
         }
      }
   )
   (dx_check_xeniawrites
      {unless $dx_event_mode
         {unless {file_exists DX_SETTINGS_FILE_PATH}
            {if $dxwelcomeseen
               {unless $dx_settings_error
                  {dx_log_writer info {sprint "Cannot write DX.dta"}}
                  {ui push_screen hint_relative_writes_screen}
               }
            }
         }
      }
   )
   (dx_check_birthday
      {unless $dx_event_mode
         ;($year $month $day $hour $minute $second)
         {get_date_time $year $month $day $hour $minute $second}
         {if
            {&&
               {! $dx_msg_birth}
               ;{> $year 2023}
               {== $month 10}
               {== $day 25}
            }
            {set $dx_msg_birth TRUE}
            {dx_passive_messenger_symbol "Happy Birthday Shiloh!"}
         }
      }
   )
   (dx_play_a_show
      {if {! $play_a_show_mentioned}
         {set $play_a_show_mentioned TRUE}
         {set $read_array ()}
         {set $song_array ()}
         {set $dtaarray ()}
         {resize $read_array 0}
         {resize $song_array 0}
         {resize $dtaarray 2}
         {set $num_voted_songs 0}
      }
      {if_else {> {size $dtaarray} 1}
         {do
            {set $dx_playing_a_show TRUE}
            {dx_presence_manager}
            {set $current_voted_song FALSE}
            {set $dx_playlist_array ()}
            {resize $dx_playlist_array 0}
            {set $dx_playlist_queue_array ()}
            {resize $dx_playlist_queue_array 0}
            {set $dx_play_a_show_required_votes 0}
            {set $dx_play_a_show_num_votes 0}
            {set $slot1_voted FALSE}
            {set $slot2_voted FALSE}
            {set $slot3_voted FALSE}
            {set $slot4_voted FALSE}
            {set $slot1_vote FALSE}
            {set $slot2_vote FALSE}
            {set $slot3_vote FALSE}
            {set $slot4_vote FALSE}
            {gamemode set_mode qp_coop}
            {if_else {== {size $dx_play_a_show_active_instruments} 0}
               {do
                  {set $rb3_songlist
                     (
                        20thcenturyboy 25or6to4 antibodies
                        beforeiforget bohemianrhapsody breakonthrough centerfold
                        chinagrove coldasice combatbaby crosstowntraffic deadendfriends
                        dontstandsoclosetome duhast everybodywantstorule flylikeaneagle
                        foolin getupstandup goodvibrationslive heartofglass hereigoagain
                        humanoid ifeelgoodalt iloverocknroll inabigcountry
                        ineedtoknow inthemeantime justlikeheaven killingloneliness
                        lastdance livinginamerica lowrider meenamora midlifecrisis
                        needyoutonight ohmygod
                        oyemiamor portionsforfoxes radarlove rainbowinthedark rehab rocklobster roundabout
                        saturdaynightsalright sisterchristian smokeonthewater somethingbigger spaceoddity
                        stopmeifyouthink thebeautifulpeople thecon thekillingmoon thelook
                        thepoweroflove thisbastardslife walkinonthesun walkoflife
                        werewolvesoflondon whipit yoshimibattles
                     )
                  }
                  {meta_performer set_song {random_elem $rb3_songlist}}
                  {set $dx_prep_show_flow TRUE}
                  {ui abstract_wipe}
                  #ifndef HX_WII
                  {ui goto_screen dx_playashow_intro_screen}
                  #else
                  {ui goto_screen song_select_enter_screen}
                  #endif
               }
               {do
                  {set $dx_in_show_flow TRUE}
                  {meta_performer set_song antibodies}
                  {ui sync_screen dx_play_a_show_screen 0}
               }
            }
         }
         {dx_passive_messenger_symbol 'No songs found'}
      }
   )
)