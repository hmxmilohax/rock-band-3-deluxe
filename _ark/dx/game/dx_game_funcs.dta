#define DX_CALLBACK_HANDLES
(
   (hit miss pass check_fc check_missed num_gems_hit num_gems_combo num_gems_miss num_gems_pass)
)
{func
   dx_add_player_sinks
   ;{dx_log_writer info {sprint "func: dx_add_player_sinks"}}
   ;{dx_log_writer info
   ;   {sprintf "Executed dx_add_player_sinks - time: %.4fms"
         {time
            {dx_active_player_var_reset}
            {beatmatch foreach_active_player $player ;dx - add our custom fc checking callbacks to the currently loaded player
               {unless {== {$player instrument} vocals}
                  {set $dx_num_active_instruments {+ $dx_num_active_instruments 1}}
               }
               {$player add_sink fc_callback DX_CALLBACK_HANDLES}
               {switch {$player instrument}
                  (bass
                     {set $bassplayeractive true}
                     {set $dx_num_active_strings {+ $dx_num_active_strings 1}}
                     {$player add_sink fc_bass_callback DX_CALLBACK_HANDLES}
                  )
                  (real_guitar
                     {set $real_guitarplayeractive true}
                     {set $dx_num_active_strings {+ $dx_num_active_strings 1}}
                     {$player add_sink fc_real_guitar_callback DX_CALLBACK_HANDLES}
                  )
                  (real_bass
                     {set $real_bassplayeractive true}
                     {set $dx_num_active_strings {+ $dx_num_active_strings 1}}
                     {$player add_sink fc_real_bass_callback DX_CALLBACK_HANDLES}
                  )
                  (keys
                     {set $keysplayeractive true}
                     {$player add_sink fc_keys_callback DX_CALLBACK_HANDLES}
                  )
                  (real_keys
                     {set $real_keysplayeractive true}
                     {$player add_sink fc_real_keys_callback DX_CALLBACK_HANDLES}
                  )
                  (guitar
                     {set $guitarplayeractive true}
                     {set $dx_num_active_strings {+ $dx_num_active_strings 1}}
                     {$player add_sink fc_guitar_callback DX_CALLBACK_HANDLES}
                  )
                  (drum
                     {set $drumplayeractive true}
                     {$player add_sink fc_drum_callback DX_CALLBACK_HANDLES}
                  )
                  (real_drum
                     {set $drumplayeractive true}
                     {$player add_sink fc_real_drum_callback DX_CALLBACK_HANDLES}
                  )
                  (vocals
                     {set $vocalsplayeractive true}
                     {set $dx_active_vocals TRUE}
                  )
               }
               {$player add_sink dx_streak_callback (hit miss pass fetch_all_streaks update_streak)}

            }
         }
   ;   }
   ;}
}
{func
   dx_send_leader_song_speed
   ;{dx_log_writer info {sprint "func: dx_send_leader_song_speed"}}
   ;{dx_log_writer info
   ;   {sprintf "Executed dx_send_leader_song_speed - time: %.4fms"
         {time
            {if {is_leader_local} ; dx - sync speeds only if currently the lobby leader
               {if {< $leaderspeed 1.001}
                  ; clamp leader speed to 1.0 if the user intended to do that
                  ; works around floating point weirdness
                  {set $leaderspeed 1.0}
               }
               {dx_log_writer beatmatch {sprint "Sending Leader Speed to peers, Leaderspeed: " $leaderspeed ""}}
               DX_DISCONNECT
               {set $speedmod $leaderspeed}
               ;{set $speedmod_top $leaderspeed}
               {unless {session_mgr is_local}
                  {session send_msg_to_all {'`' (set $speedmod  {',' $leaderspeed}) kNetReliable}}
               }
            }
         }
   ;   }
   ;}
}
{func
   dx_trainer_checker
   ;{dx_log_writer info {sprint "func: dx_trainer_checker"}}
   ;{dx_log_writer info
   ;   {sprintf "Executed dx_trainer_checker - time: %.4fms"
         {time
            {if {gamemode in_mode trainer}
               {beatmatch foreach_active_player $player
                  {switch {{$player get_user} get_slot_num}
                     ((0 1 2 3)
                        {switch {$player instrument}
                           (real_bass
                              {if {== $dx_trainer_real_bass FALSE}
                                 {set $dx_skip_game_setup TRUE}
                                 {dx_passive_messenger_symbol "No Trainer for Pro-Bass found in song."}
                                 {if {exists meta_performer} {meta_performer skip_song}}
                                 {overshell set_active_status kOvershellInShell}
                                 {overshell update_all}
                                 {meta_loading_continue_screen set state kMetaLoading_Loading}
                                 {ui goto_screen meta_loading_continue_screen}
                              }
                           )
                           (real_keys
                              {if {== $dx_trainer_real_keys FALSE}
                                 {set $dx_skip_game_setup TRUE}
                                 {dx_passive_messenger_symbol "No Trainer for Pro-Keys found in song."}
                                 {if {exists meta_performer} {meta_performer skip_song}}
                                 {overshell set_active_status kOvershellInShell}
                                 {overshell update_all}
                                 {meta_loading_continue_screen set state kMetaLoading_Loading}
                                 {ui goto_screen meta_loading_continue_screen}
                              }
                           )
                           (real_guitar
                              {if {== $dx_trainer_real_guitar FALSE}
                                 {set $dx_skip_game_setup TRUE}
                                 {dx_passive_messenger_symbol "No Trainer for Pro-Guitar found in song."}
                                 {if {exists meta_performer} {meta_performer skip_song}}
                                 {overshell set_active_status kOvershellInShell}
                                 {overshell update_all}
                                 {meta_loading_continue_screen set state kMetaLoading_Loading}
                                 {ui goto_screen meta_loading_continue_screen}
                              }
                           )
                           (real_drum
                              {if {== $dx_trainer_real_drum FALSE}
                                 {set $dx_skip_game_setup TRUE}
                                 {dx_passive_messenger_symbol "No Trainer for Pro-Drums found in song."}
                                 {if {exists meta_performer} {meta_performer skip_song}}
                                 {overshell set_active_status kOvershellInShell}
                                 {overshell update_all}
                                 {meta_loading_continue_screen set state kMetaLoading_Loading}
                                 {ui goto_screen meta_loading_continue_screen}
                              }
                           )
                           (fail kDataUnhandled)
                        }
                     )
                  }
               }
            }
         }
   ;   }
   ;}
}
{func
   dx_venue_setter
   ;{dx_log_writer info {sprint "func: dx_venue_setter"}}
   ;{dx_log_writer info
   ;   {sprintf "Executed dx_venue_setter - time: %.4fms"
         {time
            {if {session_mgr is_leader_local}
               {if_else $dx_customizer
                  {do
                     {gamecfg set_venue none}
                     {meta_performer set_venue none}
                  }
                  {if_else {== $force TRUE}
                     {switch $dx_venue
                        (small_venues {do {gamecfg set_venue {random_elem (SMALL_VENUES)}} {meta_performer set_venue {random_elem (SMALL_VENUES)}}})
                        (big_venues {do {gamecfg set_venue {random_elem (BIG_VENUES)}} {meta_performer set_venue {random_elem (BIG_VENUES)}}})
                        (arena_venues {do {gamecfg set_venue {random_elem (ARENA_VENUES)}} {meta_performer set_venue {random_elem (ARENA_VENUES)}}})
                        (festival_venues {do {gamecfg set_venue {random_elem (FESTIVAL_VENUES)}} {meta_performer set_venue {random_elem (FESTIVAL_VENUES)}}})
                        (venues_video {do {gamecfg set_venue {random_elem (VENUES_VIDEO)}} {meta_performer set_venue {random_elem (VENUES_VIDEO)}}})
                        (random
                           {if_else $dx_enable_mv_venues
                               {do
                                  {meta_performer select_random_venue}
                               }
                               {do
                                  {gamecfg set_venue {random_elem (VENUES)}}
                                  {meta_performer set_venue {random_elem (VENUES)}}
                               }
                           }
                        )
                        (none
                           {if_else {&& {! {session_mgr is_local}} {is_leader_local}}
                              {do
                                 {gamecfg set_venue {random_elem (VENUES)}}
                                 {meta_performer set_venue {random_elem (VENUES)}}
                              }
                              {if {&& {session_mgr is_local} {is_leader_local}}
                                 {gamecfg set_venue none}
                                 {meta_performer set_venue none}
                              }
                           }
                        )
                        {do {gamecfg set_venue $dx_venue} {meta_performer set_venue $dx_venue}}
                     }
                     {if_else $dx_enable_mv_venues
                        {do
                           {meta_performer select_random_venue}
                        }
                        {do
                           {gamecfg set_venue {random_elem (VENUES)}}
                           {meta_performer set_venue {random_elem (VENUES)}}
                        }
                     }
                  }
               }
               ;{dx_log_writer default {sprint "Venue set to " $dx_venue " - Forced: " $force}}
            }
         }
   ;   }
   ;}
}
{func
   dx_toggle_fail_feedback
   {do
      {if
         {exists beatmatch}
         {if
            {&&
               {! {gamemode in_mode practice}} {! {gamemode in_mode trainer}}
            }
            {foreach_int $i 0 {beatmatch num_active_players} 
               {
                  {beatmatch active_player $i} set_crowd_rating_active
                     {||
                        {modifier_mgr is_modifier_active mod_brutalmode} ; force on crowd meter if on brutal mode
                        {== $dx_crowd_meter_state dx_on}
                        {== $dx_crowd_meter_state dx_off}
                     }
               }
            }
         {{coop_track_panel find crowd_meter} set_showing {== $dx_crowd_meter_state dx_on}}
         }
      }
   }
}
{func
   dx_blink_gold_star_on
   {if {&& {! {modifier_mgr is_modifier_active mod_nohud}} $dx_gold_star_progress $dx_mtvup {! {gamemode in_mode practice}} {! {gamemode in_mode trainer}} {! {gamemode in_mode tour}} {! {gamemode in_mode campaign}}}
      {set $star_num_frame 0}
      {set $dx_num_stars {{{coop_track_panel find scoreboard} find star_display} get num_stars}}
      {if {>= $dx_num_stars 5.0}
         {cond
            ({> $dx_num_stars 5.9} {set $star_num_frame 9})
            ({> $dx_num_stars 5.8} {set $star_num_frame 8})
            ({> $dx_num_stars 5.7} {set $star_num_frame 7})
            ({> $dx_num_stars 5.6} {set $star_num_frame 6})
            ({> $dx_num_stars 5.5} {set $star_num_frame 5})
            ({> $dx_num_stars 5.4} {set $star_num_frame 4})
            ({> $dx_num_stars 5.3} {set $star_num_frame 3})
            ({> $dx_num_stars 5.2} {set $star_num_frame 2})
            ({> $dx_num_stars 5.1} {set $star_num_frame 1})
            ({>= $dx_num_stars 5.0} {set $star_num_frame 0})
         }
         {unless $star_transition_ran ;i dont want it to set a new frame until we blink off
            {set $star_transition_ran TRUE}
            {{coop_track_panel find dx_star_base.tex} iterate_refs $ref {$ref set diffuse_tex {coop_track_panel find {sprint "dx_star_gold_" $star_num_frame ".tex"}}}}
            {{coop_track_panel find score_star_frame.tex} iterate_refs $ref {$ref set diffuse_tex {coop_track_panel find {sprint "dx_star_gold_" $star_num_frame ".tex"}}}}
            {if {> $star_num_frame 0}
               {{coop_track_panel find {sprint "dx_star_gold_" {- $star_num_frame 1} ".tex"}} iterate_refs $ref {$ref set diffuse_tex {coop_track_panel find {sprint "dx_star_gold_" $star_num_frame ".tex"}}}}
               {{coop_track_panel find {sprint "dx_star_gold_" {+ $star_num_frame 1} ".tex"}} iterate_refs $ref {$ref set diffuse_tex {coop_track_panel find {sprint "dx_star_gold_" $star_num_frame ".tex"}}}}
            }
         }
      }
   }
}
{func
   dx_blink_gold_star_off
   {if {&& $dx_mtvup {! {gamemode in_mode practice}} {! {gamemode in_mode trainer}} {! {gamemode in_mode tour}} {! {gamemode in_mode campaign}} {>= $dx_num_stars 5.0}}
      {set $star_transition_ran FALSE}
      {{coop_track_panel find dx_star_base.tex} iterate_refs $ref {$ref set diffuse_tex {coop_track_panel find dx_star_base.tex}}}
      {{coop_track_panel find score_star_frame.tex} iterate_refs $ref {$ref set diffuse_tex {coop_track_panel find dx_star_base.tex}}}
      {{coop_track_panel find {sprint "dx_star_gold_" $star_num_frame ".tex"}} iterate_refs $ref {$ref set diffuse_tex {coop_track_panel find dx_star_base.tex}}}
      {if {> $star_num_frame 0}
         {{coop_track_panel find {sprint "dx_star_gold_" {- $star_num_frame 1} ".tex"}} iterate_refs $ref {$ref set diffuse_tex {coop_track_panel find dx_star_base.tex}}}
         {{coop_track_panel find {sprint "dx_star_gold_" {+ $star_num_frame 1} ".tex"}} iterate_refs $ref {$ref set diffuse_tex {coop_track_panel find dx_star_base.tex}}}
      }
   }
}