#define DX_GAME_PANEL_HANDLES
(
   (dx_on_game_enter
      {dx_trainer_checker}
      {dx_set_auto_play}
      {dx_game_var_reset}
      {unless {gamemode in_mode trainer}
         {dx_presence_manager}
         {set $dx_game_enter_count
            {+ $dx_game_enter_count 1}
         }
         {set $dx_log_counter
            {+ $dx_log_counter 1}
         }
         {dx_log_writer default {sprint "Game has been entered. Game enter count: " $dx_game_enter_count " - Gamemode: " $dx_gamemode " - Local: " {session_mgr is_local}}}
         {dx_log_writer default {sprint "Current User Info - Song Speed: " $speedmod " - Trackspeed: " $trackspeed " - Venue FPS: " $emulatefps " - Venue: " $dx_venue " - Venue Forced: " $force}}
         {dx_log_writer default {sprint "Current User Textures - Emissive: " $dx_overdrive_guitar " - Highway: " $dx_highway_guitar " - Spotlight: " $dx_streak_guitar  " - Keyboard: " $dx_keyboard " - Smasher: " $dx_smasher " - Gem: " $dx_gem " - Sustain: " $dx_sustain " - Crowd Meter Tex: " $dx_crowd_meter}}
         {dx_log_writer default {sprint "Current User Textures cont. - OD Bar: " $dx_overdrive_bar  " - Multiplier Ring: " $dx_multiplier_ring " - Track Rails: " $dx_rails " - Score Tex: " $dx_score " - Flames: " $dx_flames " - Vox Arrow: " $dx_vocal_arrow " - Vox Highway: " $dx_vocal_highway " - DX Multi Angle: " $dx_multiangle}}
         {game set_kick_autoplay {modifier_mgr is_modifier_active mod_auto_kick}} ; dx - auto kick
         {gamemode set allow_drum_fills {! {modifier_mgr is_modifier_active mod_static_fills}}} ; dx - static fills
      }
   )
   (dx_on_game_exit
      {unless {gamemode in_mode trainer}
         {set $dx_already_restarted FALSE} ; dx - track resets for no BRE modifier
         {set $dx_mtvup FALSE} ;we have to make sure and turn off mtv overlay when it is unloaded or it can crash in places where we call it (overshell)
         {dx_send_leader_song_speed}
         DX_CURRENT_SONG_CLEAR
         DX_JSON_CLEAR
      }
   )
   (dx_on_game_reset
      {unless {gamemode in_mode trainer}
         {dx_game_var_reset}
      }
   )
   (dx_on_game_over
      {unless {gamemode in_mode trainer}
         {dx_game_var_reset}
         {dx_reset_time_remaining_position}
         {set $dx_mtvup FALSE}
         {{{{gamemode get track_panel} find mtv_overlay} find fade.anim} set_frame 0} ; dx - hide the mtv overlay on lose in case it's visible
         {beatmatch foreach_active_player $player
            {dx_hide_solo_box}
         }
         {if $dx_customizer
            {game_restart}
         }
      }
   )
   (dx_on_game_won
      {unless {gamemode in_mode trainer}
         {beatmatch foreach_active_player $player
            {dx_hide_solo_box}
         }
         {dx_reset_time_remaining_position}
      }
   )
)