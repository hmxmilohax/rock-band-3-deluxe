#include ../dx/track/dx_track_panel_handles.dta
#include ../dx/track/dx_track_callback_macros.dta
#include ../dx/track/dx_track_callbacks.dta
#define TRACK_END_FRAME
(1920)
#define TRACK_PANEL_HANDLERS
((enter
      {game add_sink $this}
      {if {|| {! {gamemode in_mode trainer} {! {gamemode in_mode campaign}}}}
         {$this dx_set_scoreboard_pos}
         {$this dx_set_solo_box_pos}
         {$this dx_set_star_display_pos}
         {$this dx_force_remote_vox}
         {$this dx_set_failed_pos}
      }
      {dx_add_player_sinks}
      {dx_apply_remote_textures} ;sync highway textures with remote players
      {dx_apply_remote_track_speeds} ;sync track speeds with remote players
      {$this track_texture_reset_handler}
      {dx_track_bre_reset}
      {dx_track_angle_var_reset} ;reset the track angle just to be safe
      {unless $dx_texloadonce {dx_default_texture_var_reset}} ;reset rb3 textures if no textures have been loaded yet
      {$this
         set_showing
         {!
            {gamemode in_mode practice}}})
   (exit
      {game remove_sink $this})
   (unison_hit)
   (set_track_out)
   (set_track_in)
   (animate_track
      ($start $end $period $units))
   (animate_track_out)
   (animate_track_out_fast)
   (animate_track_in_fast)
   (finish_load
      {$this set_showing FALSE}
      {$this set_track_out})
   (intro_start
      {$this track_reset}
      {$this set_showing TRUE}
      {$this set_track_in})
   (intro_skip
      {$this track_reset}
      {$this set_showing TRUE}
      {$this set_track_out})
   (on_reset
      {beatmatch
         foreach_active_player
         $m
         {$m on_new_track}})
   (on_extend
      {$this play_intro}
      {if {! {gamemode in_mode trainer}}
         {dx_track_streak_reset}
         {dx_set_song_speed}
         {dx_toggle_fail_feedback}
         {{find_obj {{get_track_panel} loaded_dir} track_0} set bottom_y -26}
         {{find_obj {{get_track_panel} loaded_dir} track_1} set bottom_y -26}
         {{find_obj {{get_track_panel} loaded_dir} track_2} set bottom_y -26}
         {{find_obj {{get_track_panel} loaded_dir} track_3} set bottom_y -26}
         {dx_track_fc_reset}
      }
      {if
         {&&
            {exists gamemode}
            {gamemode get play_track_intro_sfx}}
         {$this play_seq track_beg.cue}}))
{new
   TrackPanel
   coop_track_panel
   DX_TRACK_PANEL_HANDLES
   DX_TRACK_TEXTURE_HANDLER
   (file
      "track/trackpanel.milo")
   TRACK_PANEL_HANDLERS}
{func
   get_track_panel
   {gamemode get track_panel}}
{set
   $playback_file
   ""}