#define DX_SET_CONTROLLER_MENU
(
   {do
      ($options
         {array 0})
      {if_else 
         {&&
            {== $dx_joypad_type_guitar kControllerGuitar}
            {== $dx_joypad_type_keys kControllerKeys}
            {== $dx_joypad_type_vocals kControllerVocals}
         }
         {do
            {push_back $options os_instrumentswap}
            {set $dx_menu_message os_instrumentswap_desc}}
         {do
            {push_back $options os_instreset} ;shows controller reset menu if at least one instrument faking is active
            {set $dx_menu_message os_instreset_desc}
         }
      }
      {push_back $options os_pad_mapping}
      {dx_state_setup dxState_ControllerMenu FALSE}
   }
)

(dxState_ControllerMenu
   (view
      DX_SET_CONTROLLER_MENU
      {overshell_view_chooser}
   )
   (enter
      DX_OS_OPEN_MSG
      DX_MENU_DESC
   )
   (SCROLL_MSG
      DX_MENU_SCROLL_DESC
   )
   (SELECT_MSG
      {switch {$component selected_sym}
         (os_instrumentswap
            {switch {{$this get_user} get_controller_type}
               (kControllerGuitar
                  {$this show_state dxState_InstrumentSwapGuitar})
               (kControllerKeys
                  {$this show_state dxState_InstrumentSwapKeys})
               (kControllerVocals
                  {$this show_state dxState_InstrumentSwapVocals}
               )
            }
         )
         (os_instreset
            DX_OS_CLOSE_MSG
            DX_RESET_INST_TYPES
            ;{$this show_state dxState_DeluxeSettings} ;force closes the menu if a different player resets the controllers
            {overshell update_all}
         )
         (os_pad_mapping
            {$this show_state dxState_PadMappingSwap}
         )
      }
   )
   (exit 
      DX_OS_CLOSE_MSG
      {dx_state_pos_tracker}
   )
   (on_cancel
      {$this show_state dxState_DeluxeSettings}
   )
)

(dxState_PadMappingSwap
   (view
      {do
         ($options
            {array 0})
         {push_back $options os_pad}
         {push_back $options os_rbguitar}
         {push_back $options os_ghguitar}
         {push_back $options os_rbdrums}
         {push_back $options os_rb2drums}
         {push_back $options os_ghdrums}
         {dx_state_setup dxState_PadMappingSwap FALSE}
      }
      {overshell_view_chooser}
   )
   (enter
      DX_OS_OPEN_MSG
      {set $dx_menu_message os_pad_mapping_select_desc}
      DX_MENU_DESC
   )
   (SELECT_MSG
      {switch {$component selected_sym}
         (os_pad
            {set $dx_joypad_pad_mapping joypad})
         (os_rbguitar
            {set $dx_joypad_pad_mapping #ifdef HX_PS3 hx_guitar_ps3 #else #ifdef HX_WII wii_guitar #else ro_guitar_xbox #endif #endif})
         (os_ghguitar
            {set $dx_joypad_pad_mapping #ifdef HX_PS3 ro_guitar_ps3 #else #ifdef HX_WII wii_guitar #else ro_guitar_xbox #endif #endif})
         (os_rbdrums
            {set $dx_joypad_pad_mapping #ifdef HX_PS3 hx_drums_ps3 #else #ifdef HX_WII wii_drums #else hx_drums_xbox #endif #endif})
         (os_rb2drums
            {set $dx_joypad_pad_mapping #ifdef HX_PS3 hx_drums_ps3 #else #ifdef HX_WII wii_drums #else hx_drums_xbox_rb2 #endif #endif})
         (os_ghdrums
            {set $dx_joypad_pad_mapping #ifdef HX_PS3 ro_drums_ps3 #else #ifdef HX_WII wii_rodrums #else ro_drums_xbox #endif #endif})
      }
      DX_REMAP_PAD
   )
   (exit 
      DX_OS_CLOSE_MSG
      {dx_state_pos_tracker}
   )
   (on_cancel
      {$this show_state dxState_ControllerMenu}
   )
)

(dxState_InstrumentSwapGuitar
   (view
      {do
         ($options
            {array 0})
         {push_back $options keys}
         {push_back $options drums}
         {push_back $options vocals}
         {dx_state_setup dxState_InstrumentSwapGuitar FALSE}
      }
      {overshell_view_chooser}
   )
   (enter
      DX_OS_OPEN_MSG
      {set $dx_menu_message os_instrumentswap_menu_guitar_desc}
      DX_MENU_DESC
   )
   (SELECT_MSG
      DX_RESET_INST_TYPES ;resets all instruments to default before faking a new one
      {switch {$component selected_sym}
         (keys
            {set $dx_joypad_type_guitar kControllerKeys})
         (drums
            {set $dx_joypad_type_guitar kControllerDrum})
         (vocals
            {set $dx_joypad_type_guitar kControllerVocals}
         )
      }
      DX_SWAP_INSTRUMENT_TYPE_GUITAR
      {dx_log_writer FALSE {sprint "Guitar instrument type has been changed to: " {find $syscfg joypad instrument_mapping ro_guitar}}}
      DX_OS_CLOSE_MSG
   )
   (exit 
      DX_OS_CLOSE_MSG
      {dx_state_pos_tracker}
   )
   (on_cancel
      {$this show_state dxState_ControllerMenu}
   )
)

(dxState_InstrumentSwapKeys
   (view
      {do
         ($options
            {array 0})
         {push_back $options guitar}
         {push_back $options drums}
         {push_back $options vocals}
         {dx_state_setup dxState_InstrumentSwapKeys FALSE}
      }
      {overshell_view_chooser}
   )
   (enter
      DX_OS_OPEN_MSG
      {set $dx_menu_message os_instrumentswap_menu_guitar_desc}
      DX_MENU_DESC
   )
   (SELECT_MSG
      DX_RESET_INST_TYPES ;resets all instruments to default before faking a new one
      {switch {$component selected_sym}
         (guitar
            {set $dx_joypad_type_keys kControllerGuitar})
         (drums
            {set $dx_joypad_type_keys kControllerDrum})
         (vocals
            {set $dx_joypad_type_keys kControllerVocals}
         )
      }
      DX_SWAP_INSTRUMENT_TYPE_KEYS
      {dx_log_writer FALSE {sprint "Keys instrument type has been changed to: " {find $syscfg joypad instrument_mapping hx_midi_keys_xbox}}}
      DX_OS_CLOSE_MSG
   )
   (exit 
      DX_OS_CLOSE_MSG
      {dx_state_pos_tracker}
   )
   (on_cancel
      {$this show_state dxState_ControllerMenu}
   )
)

(dxState_InstrumentSwapVocals
   (view
      {do
         ($options
            {array 0})
         {push_back $options guitar}
         {push_back $options keys}
         {push_back $options drums}
         {dx_state_setup dxState_InstrumentSwapVocals FALSE}
      }
      {overshell_view_chooser}
   )
   (enter
      DX_OS_OPEN_MSG
      {set $dx_menu_message os_instrumentswap_menu_guitar_desc}
      DX_MENU_DESC
   )
   (SELECT_MSG
      DX_RESET_INST_TYPES ;resets all instruments to default before faking a new one
      {switch {$component selected_sym}
         (guitar
            {set $dx_joypad_type_vocals kControllerGuitar})
         (drums
            {set $dx_joypad_type_vocals kControllerDrum})
         (keys
            {set $dx_joypad_type_vocals kControllerKeys}
         )
      }
      DX_SWAP_INSTRUMENT_TYPE_VOCALS
      {dx_log_writer FALSE {sprint "Vocals instrument type has been changed to: " {find $syscfg joypad instrument_mapping analog}}}
      DX_OS_CLOSE_MSG
   )
   (exit 
      DX_OS_CLOSE_MSG
      {dx_state_pos_tracker}
   )
   (on_cancel
      {$this show_state dxState_ControllerMenu}
   )
)