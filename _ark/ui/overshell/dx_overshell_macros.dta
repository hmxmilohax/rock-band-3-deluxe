#define DX_OS_OPEN_MSG
(
   ;opens player message panel on current player
   {if {! {session is_in_game}}
      {{overshell_player_message find show.trg} trigger}
      {overshell_player_message set_showing TRUE}
      {set $dx_no_message_sound TRUE}} ;prevents message sound from playing in menus
)
#define DX_OS_CLOSE_MSG
(
   ;closes player message panel on current player
   {if {! {session is_in_game}}
      {{overshell_player_message find show.trg} trigger}
      {overshell_player_message set_showing FALSE}
      {set $dx_menu_message ""}
      {set $dx_no_message_sound FALSE}} ;re-allows message sound to play after closing
)
#define DX_MENU_SCROLL_DESC ;used to show a description for each item on a list
(
   {if {! {session is_in_game}}
      ($this setup_message_text
         {sprint {$component selected_sym} "_desc"})} ;appends _desc to selected item for locale descriptions
)
#define DX_MENU_DESC ;used to show one static message over an entire menu ( paired with {set $dx_menu_message ...} )
(
   {if {! {session is_in_game}}
      ($this setup_message_text $dx_menu_message)}
)
#define DX_RB3E_SETTINGS_LIST ;creates list for Deluxe Settings Menu
(
   {do
      ($options
         {array 0})
         {push_back $options os_spds}
         {push_back $options os_venuesel}
         {push_back $options os_thm}
         {push_back $options os_cam}
         {push_back $options os_audiomenu}
         ;only show the controller menu when not in-game and only on applicable instruments or when faking is active
         {if
            {&&
               {! {session is_in_game}} ;prevents soft-lock, swapping instrument type in-game would prevent player from rejoining
               {||
                  {||
                     {== {{$this get_user} get_controller_type} kControllerGuitar}
                     {== {{$this get_user} get_controller_type} kControllerKeys}
                     {== {{$this get_user} get_controller_type} kControllerVocals}
                  }
                  {||
                     {! {== $dx_joypad_type_guitar kControllerGuitar}}
                     {! {== $dx_joypad_type_keys kControllerKeys}}
                     {! {== $dx_joypad_type_vocals kControllerVocals}}
                  }
               }
               {push_back $options os_ctrlmenu}
            }
         }
         $options
   }
)
#define DX_DISPLAY_MSG_HANDLER
(
   (display_trackspeed_msg ;why do these not work
      ($tspd)
      {trackspeed_msg set_token_fmt {sprintf {localize trackspeed_msg} {int {+ 0.5 {* $tspd 100}}}}})
   (display_speed_msg
      ($mspd)
     {speed_msg set_token_fmt {sprintf {localize speed_msg} {int {+ 0.5 {* $mspd 100}}}}})
   (display_speedcap_msg
      ($fspd)
     {speed_msg set_token_fmt {sprintf {localize speed_msg} {int {+ 0.5 {* $fspd 100}}}}})
   (display_fps_msg
      ($fps)
     {fps_msg set_token_fmt {sprintf {localize fps_msg} {int $fps}}})
   (set_speeds
      {if $dx_slider {switch $dx_slider_id
         (dx_speedmenu 
            {set $emulatefps {+ 1 {instruments.sld current}}}
            {set $trackspeed {- {+ 1 {* {bg_music.sld current} 0.05}} 0.5}}
            {set $speedmod {+ 1 {* {crowd.sld current} 0.05}}}
            {set $leaderspeed $speedmod}
            {set $leaderspeed $speedmod}
            {$this display_trackspeed_msg $trackspeed}
            {$this display_speed_msg $speedmod}
            {$this display_fps_msg $emulatefps}
         )
         (dx_fastermode
            {set $speedmod_cap {+ 1 {* {bg_music.sld current} 0.05}}}
            {$this display_speedcap_msg $speedmod_cap}
         )
      }}
   )
)
#define AUDIO_DATA
(
   overshell_audio_instruments
   overshell_audio_bg_music
   overshell_audio_crowd
   overshell_audio_sfx
   #ifdef HX_PS3
   overshell_audio_voice_chat
   #endif
   {if_else {modifier_mgr is_modifier_active mod_nosfx}
      os_audio_instsfx_on
      os_audio_instsfx_off
   }
)
#define DX_WARNING_HANDLER
(
   {if_else $ingameset
      {handle ($this set_confirm_message ingame_warning)}
      {if_else $author
         {do
            {set $author FALSE}
            {handle ($this set_confirm_message author_warning)}
         }
         {if_else $autoplay
            {do
               {set $autoplay FALSE}
               {handle ($this set_confirm_message autoplay_warning)}
            }
            {if_else $breakneck
               {do
                  {set $breakneck FALSE}
                  {handle ($this set_confirm_message breakneck_warning)}
               }
               {handle
                  ($this set_confirm_message disable_drum_surface_navigation_warning)}
            }
         }
      }
   }
)
#define OVERSHELL_RB4OS
(
   {background.tex set_bitmap "ui/overshell/rb4_early/background.png"}
   {connect_controller.tex set_bitmap "ui/overshell/rb4_early/connect_controller.png"}
   {connect_controller_normal2.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {connect_controller_spec.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {pause_container.tex set_bitmap "ui/overshell/rb4_early/pause_container.png"}
   {top_bg.tex set_bitmap "ui/overshell/rb4_early/top_bg.png"}
   {help_bar.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {top_help_bg.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {top_help_bg02.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {online_bg.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {bottom_bg.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {top_help_lowered.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {top_help_lowered02.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {song_bg.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {header_song_bg_normal_pma.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {side_piece.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {results_sidebar.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {top_bg.mesh set_local_scale .51 .25 .6}
   {background.mesh set_local_scale_index 0 .675}
   {background.mesh set_local_pos_index 0 1}
   {background.mesh set_local_pos_index 2 -810}
   {ingame_pause_bg.mat set color {pack_color 1.0 1.0 1.0}}
   {ingame_pausebg.mat set color {pack_color 1.0 1.0 1.0}}
)
#define OVERSHELL_RIVALSOS
(
   {background.tex set_bitmap "ui/overshell/rb4_rivals/background.png"}
   {connect_controller.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {connect_controller_normal2.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {connect_controller_spec.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {pause_container.tex set_bitmap "ui/overshell/rb4_rivals/pause_container.png"}
   {top_bg.tex set_bitmap "ui/overshell/rb4_rivals/top_bg.png"}
   {help_bar.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {top_help_bg.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {top_help_bg02.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {online_bg.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {bottom_bg.tex set_bitmap "ui/overshell/rb4_rivals/bottom_bg.png"}
   {bottom_bg.tex iterate_refs $ref {$ref set color {pack_color 1.0 1.0 1.0}}}
   {top_help_lowered02.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {top_help_lowered.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {song_bg.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {header_song_bg_normal_pma.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {side_piece.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {results_sidebar.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {top_bg.mesh set_local_scale .51 .4 .6}
   {top_bg.mesh set_local_pos_index 2 55}
   ;{background.mesh set_local_scale_index 0 .675}
   ;{background.mesh set_local_pos_index 0 1}
   ;{background.mesh set_local_pos_index 2 -810}
   {ingame_pause_bg.mat set color {pack_color 1.0 1.0 1.0}}
   {ingame_pausebg.mat set color {pack_color 1.0 1.0 1.0}}
)
#define DX_AUTO_VOCAL_PLAYER_DROPPER_HANDLER
(
   {if {> $dx_os_slot_counter 3}
      {set $dx_os_slot_counter 0}
   }
   {if {== $dx_os_slot_counter 2}
      {set $dx_os_slot_counter 3}
   }
   {if 
      {&&
         {user_mgr get_user_from_slot $dx_os_slot_counter}
         {== {{user_mgr get_user_from_slot $dx_os_slot_counter} get_controller_type} kControllerVocals}
      }
      {{overshell get_slot {user_mgr get_user_from_slot $dx_os_slot_counter}} remove_user}
   }
   {set $dx_os_slot_counter {+ $dx_os_slot_counter 1}}
)
#define DX_AUTO_VOCALS_HANDLER
(
   {if_else {modifier_mgr is_modifier_active mod_auto_vocals}
      {do
         {$this attempt_toggle_auto_vocals} ;vanilla function to turn off AIM
         ;if AIM successfully turns off, proceed to remove extra vocalists
         {if {! {modifier_mgr is_modifier_active mod_auto_vocals}}
            ;Checks whether or not a player exists and if they're a vocalist on slots 0, 1, 3 before removing them
            DX_AUTO_VOCAL_PLAYER_DROPPER_HANDLER
            DX_AUTO_VOCAL_PLAYER_DROPPER_HANDLER
            DX_AUTO_VOCAL_PLAYER_DROPPER_HANDLER
            {overshell update_all}
         }
      }
      {if_else
         ;Forces AIM to turn on, even if only a vocalist exists and requirements are met
         ;Otherwise, the vanilla function will be used to turn on AIM
         {&&
            {== {user_mgr get_num_local_participants} 1}
            {== {{$this get_user} get_controller_type} kControllerVocals}
            {! {$this in_game}}
            {session_mgr is_local}
         }
         {do
            {modifier_mgr toggle_modifier_enabled mod_auto_vocals}
            {overshell update_all}
         }
         {$this attempt_toggle_auto_vocals}
      }
   }
)
#define DX_NO_SFX_HANDLER
(
   (os_audio_instsfx_off
      {unless {modifier_mgr is_modifier_active mod_nosfx}
         {modifier_mgr toggle_modifier_enabled mod_nosfx}
         {options_audio.lst set_data (AUDIO_DATA)}
         {if $audiomenuval
            {options_audio.lst set_selected $audiomenuval}
         }
      }
   )
   (os_audio_instsfx_on
      {if {modifier_mgr is_modifier_active mod_nosfx}
         {modifier_mgr toggle_modifier_enabled mod_nosfx}
      }
      {options_audio.lst set_data (AUDIO_DATA)}
      {if $audiomenuval
         {options_audio.lst set_selected $audiomenuval}
      }
   )
)
#define DX_MODIFIER_HANDLERS
(
   (mod_nohud
      {if {modifier_mgr is_modifier_active mod_nohud}
         {if {modifier_mgr is_modifier_active mod_fakejuke}
            {modifier_mgr toggle_modifier_enabled mod_fakejuke}
            {modifier_mgr toggle_modifier_enabled mod_auto_play}
            {modifier_mgr toggle_modifier_enabled mod_nopause}
         }
      }
      {modifier_mgr toggle_modifier_enabled mod_nohud}
      {overshell update_all}
   )
   (mod_nolanes
      {if {modifier_mgr is_modifier_active mod_rb4lanes}
         {modifier_mgr toggle_modifier_enabled mod_rb4lanes}
      }
      {modifier_mgr toggle_modifier_enabled mod_nolanes}
      {set $nolanes {! $nolanes}}
      {overshell update_all}
   )
   (mod_rb4lanes
      {if {modifier_mgr is_modifier_active mod_nolanes}
         {modifier_mgr toggle_modifier_enabled mod_nolanes}
      }
      {modifier_mgr toggle_modifier_enabled mod_rb4lanes}
      {overshell update_all}
   )
   (mod_practiceoverdrive
      {modifier_mgr toggle_modifier_enabled mod_practiceoverdrive}
      {set $practiceoverdrive {! $practiceoverdrive}}
      {overshell update_all}
   )
   (mod_nobre
      {modifier_mgr toggle_modifier_enabled mod_nobre}
      {overshell update_all}
   )
   (mod_fakebrutal
      {modifier_mgr toggle_modifier_enabled mod_fakebrutal}
      {set $fakebrutalmode {! $fakebrutalmode}}
      {overshell update_all}
   )
   (mod_doublespeed
      {modifier_mgr toggle_modifier_enabled mod_doublespeed}
      {overshell update_all}
   )
   (mod_fakejuke
      {do
         {session disconnect}
         {session_mgr disconnect}
      }
      {unless {{$this get_session_mgr} is_online_enabled}
         {unless {modifier_mgr is_modifier_active mod_fakejuke}
            {$this show_state kState_FakeJukeWarn}
         }
         {if {modifier_mgr is_modifier_active mod_fakejuke}
            {do
               {modifier_mgr toggle_modifier_enabled $modifier}
               {if {modifier_mgr is_modifier_active mod_auto_play}
                  {modifier_mgr toggle_modifier_enabled mod_auto_play}
               }
               {if {modifier_mgr is_modifier_active mod_nohud}
                  {modifier_mgr toggle_modifier_enabled mod_nohud}
               }
               {if {modifier_mgr is_modifier_active mod_nopause}
                  {modifier_mgr toggle_modifier_enabled mod_nopause}
               }
               {if {modifier_mgr is_modifier_active mod_dx_no_overdrive}
                  {modifier_mgr toggle_modifier_enabled mod_dx_no_overdrive}
               }
               {overshell update_all}
               {ui goto_screen meta_loading_main_screen}
            }
         }
      }
   )
   (mod_auto_play
      {do
         {session disconnect}
         {session_mgr disconnect}
      }
      {unless {{$this get_session_mgr} is_online_enabled}
         {unless {modifier_mgr is_modifier_active mod_auto_play}
            {$this show_state kState_AutoPlayWarn}
         }
         {if {modifier_mgr is_modifier_active mod_auto_play}
            {do
               {modifier_mgr toggle_modifier_enabled $modifier}
               {if {modifier_mgr is_modifier_active mod_fakejuke}
                  {modifier_mgr toggle_modifier_enabled mod_fakejuke}
                  {modifier_mgr toggle_modifier_enabled mod_nohud}
                  {modifier_mgr toggle_modifier_enabled mod_nopause}
               }
               {overshell update_all}
               {ui goto_screen meta_loading_main_screen}
            }
         }
      }
   )
   (mod_songtitlestats
      {do
         {if_else {&& {modifier_mgr is_modifier_active mod_current_song_logging} {modifier_mgr is_modifier_active mod_songtitlestats}}
            {do
               {modifier_mgr toggle_modifier_enabled $modifier}
               {modifier_mgr toggle_modifier_enabled mod_current_song_logging} ;turn off both modifiers if both modifiers are on
            }
            {modifier_mgr toggle_modifier_enabled $modifier}
         }
         {overshell update_all}
      }
   )
   (mod_current_song_logging
      {do
         {modifier_mgr toggle_modifier_enabled $modifier}
         {unless {modifier_mgr is_modifier_active mod_songtitlestats}
            {modifier_mgr toggle_modifier_enabled mod_songtitlestats} ;turn on song title stats if using this modifier
         }
         {unless {modifier_mgr is_modifier_active mod_dx_logging}
            {modifier_mgr toggle_modifier_enabled mod_dx_logging} ;turn on song title stats if using this modifier
         }
         {overshell update_all}
      }
   )
   (mod_dx_logging
      {do
         {modifier_mgr toggle_modifier_enabled $modifier}
         {unless {modifier_mgr is_modifier_active mod_songtitlestats}
            {modifier_mgr toggle_modifier_enabled mod_songtitlestats} ;turn on song title stats if using this modifier
         }
         {unless {modifier_mgr is_modifier_active mod_current_song_logging}
            {modifier_mgr toggle_modifier_enabled mod_current_song_logging} ;turn on song title stats if using this modifier
         }
         {overshell update_all}
      }
   )
   (mod_songtitle
      {do
         {modifier_mgr toggle_modifier_enabled $modifier}
         {if {modifier_mgr is_modifier_active mod_no_mtv_overlay}
            {modifier_mgr toggle_modifier_enabled mod_no_mtv_overlay} ;turn on song title stats if using this modifier
         }
         {if
            {exists beatmatch}
            {if
               {||
                  {gamemode in_mode defaults} {gamemode in_mode qp_party_shuffle}
                  {gamemode in_mode qp_coop} {gamemode in_mode campaign}
                  {gamemode in_mode party_shuffle} {gamemode in_mode tour}
               }
               ;{{coop_track_panel find crowd_meter} set_showing {! {modifier_mgr is_modifier_active mod_no_crowd_meter}}}
               ;{{coop_track_panel find scoreboard} set_showing {! {modifier_mgr is_modifier_active mod_no_scoreboard}}}
               {{{gamemode get track_panel} find mtv_overlay} intro_end}
            }
         }
         {overshell update_all}
      }
   )
   (mod_no_mtv_overlay
      {do
         {modifier_mgr toggle_modifier_enabled $modifier}
         {if {modifier_mgr is_modifier_active mod_songtitle}
            {modifier_mgr toggle_modifier_enabled mod_songtitle} ;turn on song title stats if using this modifier
         }
         {if
            {exists beatmatch}
            {if
               {||
                  {gamemode in_mode defaults} {gamemode in_mode qp_party_shuffle}
                  {gamemode in_mode qp_coop} {gamemode in_mode campaign}
                  {gamemode in_mode party_shuffle} {gamemode in_mode tour}
               }
               ;{{coop_track_panel find crowd_meter} set_showing {! {modifier_mgr is_modifier_active mod_no_crowd_meter}}}
               ;{{coop_track_panel find scoreboard} set_showing {! {modifier_mgr is_modifier_active mod_no_scoreboard}}}
               {{{gamemode get track_panel} find mtv_overlay} intro_end}
            }
         }
         {overshell update_all}
      }
   )
   (mod_no_scoreboard
      {do
         {modifier_mgr toggle_modifier_enabled $modifier}
         {if
            {exists beatmatch}
            {if
               {||
                  {gamemode in_mode defaults} {gamemode in_mode qp_party_shuffle}
                  {gamemode in_mode qp_coop} {gamemode in_mode campaign}
                  {gamemode in_mode party_shuffle} {gamemode in_mode tour}
               }
               ;{{coop_track_panel find crowd_meter} set_showing {! {modifier_mgr is_modifier_active mod_no_crowd_meter}}}
               {{coop_track_panel find scoreboard} set_showing {! {modifier_mgr is_modifier_active mod_no_scoreboard}}}
               ;{{{gamemode get track_panel} find mtv_overlay} intro_end}
            }
         }
         {overshell update_all}
      }
   )
   (mod_no_crowd_meter
      {do
         {modifier_mgr toggle_modifier_enabled $modifier}
         {if
            {exists beatmatch}
            {if
               {||
                  {gamemode in_mode defaults} {gamemode in_mode qp_party_shuffle}
                  {gamemode in_mode qp_coop} {gamemode in_mode campaign}
                  {gamemode in_mode party_shuffle} {gamemode in_mode tour}
               }
               {{coop_track_panel find crowd_meter} set_showing {! {modifier_mgr is_modifier_active mod_no_crowd_meter}}}
               ;{{coop_track_panel find scoreboard} set_showing {! {modifier_mgr is_modifier_active mod_no_scoreboard}}}
               ;{{{gamemode get track_panel} find mtv_overlay} intro_end}
            }
         }
         {overshell update_all}
      }
   )
   (mod_auto_kick
      {do
         {session disconnect}
         {session_mgr disconnect}
         {modifier_mgr toggle_modifier_enabled $modifier}
         {if {$this in_game}
            {$this show_state kState_ModifierSongLoad}
         }
         {overshell update_all}
      }
   )
   (mod_chmode
      {unless {modifier_mgr is_modifier_active mod_chmode}
         {$this show_state kState_CHModeWarn}
      }
      {if {modifier_mgr is_modifier_active mod_chmode}
         {do
            {modifier_mgr toggle_modifier_enabled $modifier}
            {set $railscustomtexture TRUE}
            {if {== $rails none}
               {set $rails rb3}
            }
            {overshell update_all}
            {ui goto_screen meta_loading_main_screen}
         }
      }
   )
   (mod_whammyfx
      {modifier_mgr toggle_modifier_enabled $modifier}
      {if {$this in_game}
         {$this show_state kState_ModifierSongLoad}
      }
      {overshell update_all}
   )
   (mod_nomv
      {modifier_mgr toggle_modifier_enabled mod_nomv}
      {overshell update_all}
      {if_else {$this in_game}
         {$this show_state kState_ModifierSongLoad}
         {ui goto_screen meta_loading_main_screen}
      }
   )
   (mod_noblur
      {if_else {modifier_mgr is_modifier_active mod_noblur}
         {do 
            {set $blurisbackon TRUE}
            {modifier_mgr toggle_modifier_enabled mod_noblur}
         }
         {do
            {set $blurisbackon FALSE}
            {modifier_mgr toggle_modifier_enabled mod_noblur}
         }
      }
      {overshell update_all}
   )
   (mod_nocrust
      {if_else {modifier_mgr is_modifier_active mod_nocrust}
         {do 
            {set $crustisbackon TRUE}
            {modifier_mgr toggle_modifier_enabled mod_nocrust}
         }
         {do
            {set $crustisbackon FALSE}
            {modifier_mgr toggle_modifier_enabled mod_nocrust}
         }
      }
      {overshell update_all}
   )
   (mod_blackmenu
      {modifier_mgr toggle_modifier_enabled mod_blackmenu}
      {if_else {$this in_game}
         {$this show_state kState_ModifierSongLoad}
         {ui goto_screen meta_loading_main_screen}
      }
   )
   (mod_calibration
      {unless
         {modifier_mgr is_modifier_active mod_calibration}
         {$this show_state kState_CalibrationWarn}
      }
      {if
         {modifier_mgr is_modifier_active mod_calibration}
         {do
            {modifier_mgr toggle_modifier_enabled $modifier}
            {overshell update_all}
         }
      }
   )
)

;part selection, adds non-standard selections on a certain instrument if faking is active
#define DX_PART_SELECT_GUITAR
(
   {do
      ($options
         {array 0})
      {push_back $options overshell_guitar}
      {push_back $options overshell_bass}
      {push_back $options overshell_keys}
      {if {== $dx_joypad_type_vocals kControllerGuitar}
         {do
            {push_back $options overshell_vocal_solo}
            {push_back $options overshell_vocal_harmony}
         }
      }
      {choose_part.lst set_data $options}
   }
)
#define DX_PART_SELECT_DRUMS
(
   {do
      ($options
         {array 0})
      {push_back $options overshell_drums}
      {push_back $options overshell_drums_pro}
      {if 
         {||
            {== $dx_joypad_type_guitar kControllerDrum}
            {== $dx_joypad_type_keys kControllerDrum}
            {== $dx_joypad_type_vocals kControllerDrum}
         }
         {do
            {push_back $options overshell_guitar}
            {push_back $options overshell_bass}
            {push_back $options overshell_keys}
         }
      }
      {if {== $dx_joypad_type_keys kControllerDrum}
         {push_back $options overshell_real_keys}
      }
      {if {== $dx_joypad_type_vocals kControllerDrum}
         {do
            {push_back $options overshell_vocal_solo}
            {push_back $options overshell_vocal_harmony}
         }
      }
      {choose_part.lst set_data $options}
   }
)
#define DX_PART_SELECT_VOCALS
(
   {do
      ($options
         {array 0})
      {push_back $options overshell_vocal_solo}
      {push_back $options overshell_vocal_harmony}
      {push_back $options overshell_guitar}
      {push_back $options overshell_bass}
      {push_back $options overshell_keys}
      {if {== $dx_joypad_type_keys kControllerVocals}
         {push_back $options overshell_real_keys}}
      {push_back $options overshell_drums}
      {choose_part.lst set_data $options}
   }
)
#define DX_PART_SELECT_KEYS
(
   {do
      ($options
         {array 0})
      {push_back $options overshell_keys}
      {push_back $options overshell_real_keys}
      {push_back $options overshell_guitar}
      {push_back $options overshell_bass}
      {push_back $options overshell_drums}
      {if {== $dx_joypad_type_vocals kControllerKeys}
         {do
            {push_back $options overshell_vocal_solo}
            {push_back $options overshell_vocal_harmony}
         }
      }
      {choose_part.lst set_data $options}
   }
)
#define DX_PART_SELECT_REAL_GUITAR
({choose_part.lst set_data (overshell_real_guitar overshell_real_bass overshell_guitar overshell_bass)})
#define DX_PART_SELECT_AUTOPLAY
({choose_part.lst set_data (overshell_vocal_solo overshell_vocal_harmony overshell_guitar overshell_bass overshell_drums overshell_drums_pro overshell_keys overshell_real_keys overshell_real_guitar overshell_real_bass overshell_real_guitar_22 overshell_real_bass_22)})
#define DX_PART_SELECT_HANDLERS
(
   {if_else {modifier_mgr is_modifier_active mod_auto_play}
      ;makes every instrument selectable when autoplay is on
      DX_PART_SELECT_AUTOPLAY
      ;otherwise set instrument selection normally
      {do
         {switch {{$this get_user} get_controller_type}
            (kControllerGuitar DX_PART_SELECT_GUITAR)
            (kControllerDrum DX_PART_SELECT_DRUMS)
            (kControllerVocals DX_PART_SELECT_VOCALS)
            (kControllerKeys DX_PART_SELECT_KEYS)
            (kControllerRealGuitar DX_PART_SELECT_REAL_GUITAR)
            {fail "No choose part view for this controller type"}
         }
      }
   }
   ;Selects default instrument by slot number
   {switch
      {$this get_slot_num}
      (0 {choose_part.lst set_selected $default_slot0})
      (1 {choose_part.lst set_selected $default_slot1})
      (2 {choose_part.lst set_selected $default_slot2})
      (3 {choose_part.lst set_selected $default_slot3})
      {fail {choose_part.lst set_selected $default_slot0}}
   }
)
#define DX_REMAP_PAD
(
   ;Selects current mapping configuration from pads and removes them from array
   {remove_elem {find $syscfg joypad controller_mapping digital} {elem {find $syscfg joypad controller_mapping digital} 1}}
   {remove_elem {find $syscfg joypad controller_mapping analog} {elem {find $syscfg joypad controller_mapping analog} 1}}
   {remove_elem {find $syscfg joypad controller_mapping dualshock} {elem {find $syscfg joypad controller_mapping dualshock} 1}}
   {remove_elem {find $syscfg joypad controller_mapping stagekit_xbox} {elem {find $syscfg joypad controller_mapping stagekit_xbox} 1}}
   
   ;Appends newly selected map configuration to pad controller arrays
   {push_back {find $syscfg joypad controller_mapping digital} $dx_joypad_pad_mapping}
   {push_back {find $syscfg joypad controller_mapping analog} $dx_joypad_pad_mapping}
   {push_back {find $syscfg joypad controller_mapping dualshock} $dx_joypad_pad_mapping}
   {push_back {find $syscfg joypad controller_mapping stagekit_xbox} $dx_joypad_pad_mapping}
   #ifdef HX_WII
   {remove_elem {find $syscfg joypad controller_mapping wii_remote} {elem {find $syscfg joypad controller_mapping wii_remote} 1}}
   {remove_elem {find $syscfg joypad controller_mapping wii_classic} {elem {find $syscfg joypad controller_mapping wii_classic} 1}}
   {remove_elem {find $syscfg joypad controller_mapping gamecube} {elem {find $syscfg joypad controller_mapping gamecube} 1}}
   {push_back {find $syscfg joypad controller_mapping wii_remote} {if_else {== $dx_joypad_pad_mapping joypad} wii_remote $dx_joypad_pad_mapping}}
   {push_back {find $syscfg joypad controller_mapping wii_classic} {if_else {== $dx_joypad_pad_mapping joypad} wii_classic $dx_joypad_pad_mapping}}
   {push_back {find $syscfg joypad controller_mapping gamecube} {if_else {== $dx_joypad_pad_mapping joypad} gamecube $dx_joypad_pad_mapping}}
   #endif
   {dx_log_writer {sprint "Pad mapping has been changed to: " {find $syscfg joypad controller_mapping analog}}}
   {overshell update_all}
)
#define DX_SWAP_INSTRUMENT_TYPE_GUITAR
(
   ;Selects current instrument configuration from guitars and removes them from array
   {remove_elem {find $syscfg joypad instrument_mapping ro_guitar} {elem {find $syscfg joypad instrument_mapping ro_guitar} 1}}
   {remove_elem {find $syscfg joypad instrument_mapping ro_guitar_xbox} {elem {find $syscfg joypad instrument_mapping ro_guitar_xbox} 1}}
   {remove_elem {find $syscfg joypad instrument_mapping strat_xbox} {elem {find $syscfg joypad instrument_mapping strat_xbox} 1}}
   {remove_elem {find $syscfg joypad instrument_mapping strat_xbox_rb2} {elem {find $syscfg joypad instrument_mapping strat_xbox_rb2} 1}}
   {remove_elem {find $syscfg joypad instrument_mapping hx_guitar_ps3} {elem {find $syscfg joypad instrument_mapping hx_guitar_ps3} 1}}
   {remove_elem {find $syscfg joypad instrument_mapping ps3_rb2_guitar} {elem {find $syscfg joypad instrument_mapping ps3_rb2_guitar} 1}}
   {remove_elem {find $syscfg joypad instrument_mapping ro_guitar_ps3} {elem {find $syscfg joypad instrument_mapping ro_guitar_ps3} 1}}
   #ifdef HX_WII
   {remove_elem {find $syscfg joypad instrument_mapping wii_guitar} {elem {find $syscfg joypad instrument_mapping wii_guitar} 1}}
   {remove_elem {find $syscfg joypad instrument_mapping wii_guitar_rb2} {elem {find $syscfg joypad instrument_mapping wii_guitar_rb2} 1}}
   {remove_elem {find $syscfg joypad instrument_mapping wii_roguitar} {elem {find $syscfg joypad instrument_mapping wii_roguitar} 1}}
   #endif
   
   ;Appends newly selected instrument configuration to guitar instrument arrays
   {push_back {find $syscfg joypad instrument_mapping ro_guitar} $dx_joypad_type_guitar}
   {push_back {find $syscfg joypad instrument_mapping ro_guitar_xbox} $dx_joypad_type_guitar}
   {push_back {find $syscfg joypad instrument_mapping strat_xbox} $dx_joypad_type_guitar}
   {push_back {find $syscfg joypad instrument_mapping strat_xbox_rb2} $dx_joypad_type_guitar}
   {push_back {find $syscfg joypad instrument_mapping hx_guitar_ps3} $dx_joypad_type_guitar}
   {push_back {find $syscfg joypad instrument_mapping ps3_rb2_guitar} $dx_joypad_type_guitar}
   {push_back {find $syscfg joypad instrument_mapping ro_guitar_ps3} $dx_joypad_type_guitar}
   #ifdef HX_WII
   {push_back {find $syscfg joypad instrument_mapping wii_guitar} $dx_joypad_type_guitar}
   {push_back {find $syscfg joypad instrument_mapping wii_guitar_rb2} $dx_joypad_type_guitar}
   {push_back {find $syscfg joypad instrument_mapping wii_roguitar} $dx_joypad_type_guitar}
   #endif
   {overshell update_all}
)
#define DX_SWAP_INSTRUMENT_TYPE_KEYS
(
   ;Selects current instrument configuration from keys and removes them from array
   {remove_elem {find $syscfg joypad instrument_mapping hx_midi_keys_xbox} {elem {find $syscfg joypad instrument_mapping hx_midi_keys_xbox} 1}}
   {remove_elem {find $syscfg joypad instrument_mapping hx_midi_keys_ps3} {elem {find $syscfg joypad instrument_mapping hx_midi_keys_ps3} 1}}
   #ifdef HX_WII
   {remove_elem {find $syscfg joypad instrument_mapping hx_midi_keys_wii} {elem {find $syscfg joypad instrument_mapping hx_midi_keys_wii} 1}}
   #endif
   
   ;Appends newly selected instrument configuration to keys instrument arrays
   {push_back {find $syscfg joypad instrument_mapping hx_midi_keys_xbox} $dx_joypad_type_keys}
   {push_back {find $syscfg joypad instrument_mapping hx_midi_keys_ps3} $dx_joypad_type_keys}
   #ifdef HX_WII
   {push_back {find $syscfg joypad instrument_mapping hx_midi_keys_wii} $dx_joypad_type_keys}
   #endif
   {overshell update_all}
)
#define DX_SWAP_INSTRUMENT_TYPE_VOCALS
(
   ;Selects current instrument configuration from vocals and removes them from array
   {remove_elem {find $syscfg joypad instrument_mapping hx_xbox} {elem {find $syscfg joypad instrument_mapping hx_xbox} 1}}
   {remove_elem {find $syscfg joypad instrument_mapping digital} {elem {find $syscfg joypad instrument_mapping digital} 1}}
   {remove_elem {find $syscfg joypad instrument_mapping analog} {elem {find $syscfg joypad instrument_mapping analog} 1}}
   {remove_elem {find $syscfg joypad instrument_mapping dualshock} {elem {find $syscfg joypad instrument_mapping dualshock} 1}}
   {remove_elem {find $syscfg joypad instrument_mapping stagekit_xbox} {elem {find $syscfg joypad instrument_mapping stagekit_xbox} 1}}
   #ifdef HX_WII
   {remove_elem {find $syscfg joypad instrument_mapping wii_remote} {elem {find $syscfg joypad instrument_mapping wii_remote} 1}}
   {remove_elem {find $syscfg joypad instrument_mapping wii_classic} {elem {find $syscfg joypad instrument_mapping wii_classic} 1}}
   {remove_elem {find $syscfg joypad instrument_mapping gamecube} {elem {find $syscfg joypad instrument_mapping gamecube} 1}}
   #endif
   
   ;Appends newly selected instrument configuration to vocals instrument arrays
   {push_back {find $syscfg joypad instrument_mapping hx_xbox} $dx_joypad_type_vocals}
   {push_back {find $syscfg joypad instrument_mapping digital} $dx_joypad_type_vocals}
   {push_back {find $syscfg joypad instrument_mapping analog} $dx_joypad_type_vocals}
   {push_back {find $syscfg joypad instrument_mapping dualshock} $dx_joypad_type_vocals}
   {push_back {find $syscfg joypad instrument_mapping stagekit_xbox} $dx_joypad_type_vocals}
   #ifdef HX_WII
   {push_back {find $syscfg joypad instrument_mapping wii_remote} $dx_joypad_type_vocals}
   {push_back {find $syscfg joypad instrument_mapping wii_classic} $dx_joypad_type_vocals}
   {push_back {find $syscfg joypad instrument_mapping gamecube} $dx_joypad_type_vocals}
   #endif
   {overshell update_all}
)
#define DX_RESET_INST_TYPES ;sets all controllers back to their default instrument
(
   {set $dx_joypad_type_guitar kControllerGuitar}
   {set $dx_joypad_type_keys kControllerKeys}
   {set $dx_joypad_type_vocals kControllerVocals}
   DX_SWAP_INSTRUMENT_TYPE_GUITAR
   DX_SWAP_INSTRUMENT_TYPE_KEYS
   DX_SWAP_INSTRUMENT_TYPE_VOCALS
)