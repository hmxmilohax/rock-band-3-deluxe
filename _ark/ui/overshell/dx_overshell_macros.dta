#define DX_OS_OPEN_MSG
(
   ;opens player message panel on current player
   {if {! {session is_in_game}}
      {{overshell_player_message find show.trg} trigger}
      {overshell_player_message set_showing TRUE}
      {set $dx_no_message_sound TRUE}} ;prevents message sound from playing in menus
)
#define DX_OS_CLOSE_MSG
(
   ;closes player message panel on current player
   {if {! {session is_in_game}}
      {{overshell_player_message find show.trg} trigger}
      {overshell_player_message set_showing FALSE}
      {set $dx_no_message_sound FALSE}} ;re-allows message sound to play after closing
)
#define DX_MENU_SCROLL_DESC
(
   {if {! {session is_in_game}}
      ($this setup_message_text
         {sprint {$component selected_sym} "_desc"})} ;appends _desc to selected item for locale descriptions
)
#define DX_MENU_DESC
(
   {if {! {session is_in_game}}
      ($this setup_message_text $dx_menu_message)}
)
#define DX_RESET_INST_TYPES
(
   {set $dx_joypad_type_guitar kControllerGuitar}
   {set $dx_joypad_type_keys kControllerKeys}
   {set $dx_joypad_type_vocals kControllerVocals}
)
#define DX_GET_NAV_TYPE ;selects navigation sfx based on controller, prevents crashing when swapping instrument type
(
   {switch
      $nav_type
      (ro_guitar guitar)
      (ro_guitar_xbox guitar)
      (strat_xbox guitar)
      (strat_xbox_rb2 guitar)
      (hx_guitar_ps3 guitar)
      (ps3_rb2_guitar guitar)
      (ro_guitar_ps3 guitar)
      (hx_midi_guitar_xbox real_guitar)
      (hx_midi_keys_xbox keys)
      (hx_midi_guitar_ps3 real_guitar)
      (hx_midi_keys_ps3 keys)
      (hx_midi_guitar_wii real_guitar)
      (hx_midi_keys_wii keys)
      (midi drum)
      (hx_drums_xbox drum)
      (hx_drums_xbox_rb2 drum)
      (ro_drums_xbox drum)
      (hx_drums_ps3 drum)
      (hx_drums_ps3_rb2 drum)
      (ro_drums_ps3_ghwt drum)
      (konami_drums_ps3_rr drum)
      (ps3_rb2_drums drum)
      (hx_xbox vocals)
      (digital vocals)
      (analog vocals)
      (dualshock vocals)
      (stagekit_xbox vocals)
      #ifdef HX_WII
      (wii_remote vocals)
      (wii_classic vocals)
      (gamecube vocals)
      (wii_guitar guitar)
      (wii_guitar_rb2 guitar)
      (wii_drums drum)
      (wii_drums_rb2 drum)
      (wii_roguitar guitar)
      (wii_rodrums drum)
      #endif
   }
)
#define DX_RB3E_SETTINGS_LIST ;creates list for Deluxe Settings Menu
(
   {do
      ($options
         {array 0})
         {push_back $options os_spds}
         {push_back $options os_venuesel}
         {push_back $options os_thm}
         {push_back $options os_cam}
         {push_back $options os_audiomenu}
         ;only show the controller menu on applicable instruments or when faking is active
         {if
            {||
               {||
                  {== {{$this get_user} get_controller_type} kControllerGuitar}
                  {== {{$this get_user} get_controller_type} kControllerKeys}
                  {== {{$this get_user} get_controller_type} kControllerVocals}
               }
               {||
                  {! {== $dx_joypad_type_guitar kControllerGuitar}}
                  {! {== $dx_joypad_type_keys kControllerKeys}}
                  {! {== $dx_joypad_type_vocals kControllerVocals}}
               }
            }
            {do {push_back $options os_ctrlmenu}}
         }
         {#ifdef HX_WII profiles_wii.lst #else options_audio.lst #endif set_data $options}
   }
)
#define DX_DISPLAY_MSG_HANDLER
(
   (display_trackspeed_msg ;why do these not work
      ($tspd)
      {trackspeed_msg set_token_fmt {sprintf {localize trackspeed_msg} {int {+ 0.5 {* $tspd 100}}}}})
   (display_speed_msg
      ($mspd)
     {speed_msg set_token_fmt {sprintf {localize speed_msg} {int {+ 0.5 {* $mspd 100}}}}})
   (display_speedcap_msg
      ($fspd)
     {speed_msg set_token_fmt {sprintf {localize speed_msg} {int {+ 0.5 {* $fspd 100}}}}})
   (display_fps_msg
      ($fps)
     {fps_msg set_token_fmt {sprintf {localize fps_msg} {int $fps}}})
   (set_speeds
      {if $dx_slider {switch $dx_slider_id
         (dx_speedmenu 
            {set $emulatefps {+ 1 {instruments.sld current}}}
            {set $trackspeed {- {+ 1 {* {bg_music.sld current} 0.05}} 0.5}}
            {set $speedmod {+ 1 {* {crowd.sld current} 0.05}}}
            {set $leaderspeed $speedmod}
            {set $leaderspeed $speedmod}
            {$this display_trackspeed_msg $trackspeed}
            {$this display_speed_msg $speedmod}
            {$this display_fps_msg $emulatefps}
         )
         (dx_fastermode
            {set $speedmod_cap {+ 1 {* {bg_music.sld current} 0.05}}}
            {$this display_speedcap_msg $speedmod_cap}
         )
      }}
   )
)
#define AUDIO_DATA
(
   overshell_audio_instruments
   overshell_audio_bg_music
   overshell_audio_crowd
   overshell_audio_sfx
   #ifdef HX_PS3
   overshell_audio_voice_chat
   #endif
   {if_else {modifier_mgr is_modifier_active mod_nosfx}
      os_audio_instsfx_on
      os_audio_instsfx_off
   }
)
#define DX_WARNING_HANDLER
(
   {if_else $ingameset
      {handle ($this set_confirm_message ingame_warning)}
      {if_else $author
         {do
            {set $author FALSE}
            {handle ($this set_confirm_message author_warning)}
         }
         {if_else $autoplay
            {do
               {set $autoplay FALSE}
               {handle ($this set_confirm_message autoplay_warning)}
            }
            {if_else $breakneck
               {do
                  {set $breakneck FALSE}
                  {handle ($this set_confirm_message breakneck_warning)}
               }
               {handle
                  ($this set_confirm_message disable_drum_surface_navigation_warning)}
            }
         }
      }
   }
)
#define OVERSHELL_RB4OS
(
   {background.tex set_bitmap "ui/overshell/rb4_early/background.png"}
   {checked.tex set_bitmap "ui/overshell/rb4_early/checkmark.png"}
   {checkbox.tex set_bitmap "ui/overshell/rb4_early/checkmark_bg.png"}
   {connect_controller.tex set_bitmap "ui/overshell/rb4_early/connect_controller.png"}
   {connect_controller_normal2.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {connect_controller_spec.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {pause_container.tex set_bitmap "ui/overshell/rb4_early/pause_container.png"}
   {top_bg.tex set_bitmap "ui/overshell/rb4_early/top_bg.png"}
   {help_bar.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {top_help_bg.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {top_help_bg02.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {online_bg.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {bottom_bg.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {top_help_lowered.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {top_help_lowered02.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {song_bg.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {header_song_bg_normal_pma.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {side_piece.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {results_sidebar.tex set_bitmap ui/track/custom_track_textures/_no_highway.png}
   {top_bg.mesh set_local_scale .51 .25 .6}
   {background.mesh set_local_scale_index 0 .675}
   {background.mesh set_local_pos_index 0 1}
   {background.mesh set_local_pos_index 2 -810}
   {ingame_pause_bg.mat set color {pack_color 1.0 1.0 1.0}}
   {ingame_pausebg.mat set color {pack_color 1.0 1.0 1.0}}
)
#define DX_AUTO_VOCAL_PLAYER_DROPPER_HANDLER
(
   {if {> $dx_os_slot_counter 3}
      {set $dx_os_slot_counter 0}
   }
   {if {== $dx_os_slot_counter 2}
      {set $dx_os_slot_counter 3}
   }
   {if 
      {&&
         {user_mgr get_user_from_slot $dx_os_slot_counter}
         {== {{user_mgr get_user_from_slot $dx_os_slot_counter} get_controller_type} kControllerVocals}
      }
      {{overshell get_slot {user_mgr get_user_from_slot $dx_os_slot_counter}} remove_user}
   }
   {set $dx_os_slot_counter {+ $dx_os_slot_counter 1}}
)
#define DX_AUTO_VOCALS_HANDLER
(
   {if_else {modifier_mgr is_modifier_active mod_auto_vocals}
      {do
         {$this attempt_toggle_auto_vocals} ;vanilla function to turn off AIM
         ;if AIM successfully turns off, proceed to remove extra vocalists
         {if {! {modifier_mgr is_modifier_active mod_auto_vocals}}
            ;Checks whether or not a player exists and if they're a vocalist on slots 0, 1, 3 before removing them
            DX_AUTO_VOCAL_PLAYER_DROPPER_HANDLER
            DX_AUTO_VOCAL_PLAYER_DROPPER_HANDLER
            DX_AUTO_VOCAL_PLAYER_DROPPER_HANDLER
            {overshell update_all}
         }
      }
      {if_else
         ;Forces AIM to turn on, even if only a vocalist exists and requirements are met
         ;Otherwise, the vanilla function will be used to turn on AIM
         {&&
            {== {user_mgr get_num_local_participants} 1}
            {== {{$this get_user} get_controller_type} kControllerVocals}
            {! {$this in_game}}
            {session_mgr is_local}
         }
         {do
            {modifier_mgr toggle_modifier_enabled mod_auto_vocals}
            {overshell update_all}
         }
         {$this attempt_toggle_auto_vocals}
      }
   }
)
#define DX_NO_SFX_HANDLER
(
   (os_audio_instsfx_off
      {unless {modifier_mgr is_modifier_active mod_nosfx}
         {modifier_mgr toggle_modifier_enabled mod_nosfx}
         {options_audio.lst set_data (AUDIO_DATA)}
         {if $audiomenuval
            {options_audio.lst set_selected $audiomenuval}
         }
      }
   )
   (os_audio_instsfx_on
      {if {modifier_mgr is_modifier_active mod_nosfx}
         {modifier_mgr toggle_modifier_enabled mod_nosfx}
      }
      {options_audio.lst set_data (AUDIO_DATA)}
      {if $audiomenuval
         {options_audio.lst set_selected $audiomenuval}
      }
   )
)
#define DX_MODIFIER_HANDLERS
(
   (mod_nohud
      {if {modifier_mgr is_modifier_active mod_nohud}
         {if {modifier_mgr is_modifier_active mod_fakejuke}
            {modifier_mgr toggle_modifier_enabled mod_fakejuke}
            {modifier_mgr toggle_modifier_enabled mod_auto_play}
            {modifier_mgr toggle_modifier_enabled mod_nopause}
         }
      }
      {modifier_mgr toggle_modifier_enabled mod_nohud}
      {overshell update_all}
   )
   (mod_nolanes
      {if {modifier_mgr is_modifier_active mod_rb4lanes}
         {modifier_mgr toggle_modifier_enabled mod_rb4lanes}
      }
      {modifier_mgr toggle_modifier_enabled mod_nolanes}
      {set $nolanes {! $nolanes}}
      {overshell update_all}
   )
   (mod_rb4lanes
      {if {modifier_mgr is_modifier_active mod_nolanes}
         {modifier_mgr toggle_modifier_enabled mod_nolanes}
      }
      {modifier_mgr toggle_modifier_enabled mod_rb4lanes}
      {overshell update_all}
   )
   (mod_practiceoverdrive
      {modifier_mgr toggle_modifier_enabled mod_practiceoverdrive}
      {set $practiceoverdrive {! $practiceoverdrive}}
      {overshell update_all}
   )
   (mod_nobre
      {modifier_mgr toggle_modifier_enabled mod_nobre}
      {overshell update_all}
   )
   (mod_fakebrutal
      {modifier_mgr toggle_modifier_enabled mod_fakebrutal}
      {set $fakebrutalmode {! $fakebrutalmode}}
      {overshell update_all}
   )
   (mod_doublespeed
      {modifier_mgr toggle_modifier_enabled mod_doublespeed}
      {overshell update_all}
   )
   (mod_fakejuke
      {do
         {session disconnect}
         {session_mgr disconnect}
      }
      {unless {{$this get_session_mgr} is_online_enabled}
         {unless {modifier_mgr is_modifier_active mod_fakejuke}
            {$this show_state kState_FakeJukeWarn}
         }
         {if {modifier_mgr is_modifier_active mod_fakejuke}
            {do
               {modifier_mgr toggle_modifier_enabled $modifier}
               {if {modifier_mgr is_modifier_active mod_auto_play}
                  {modifier_mgr toggle_modifier_enabled mod_auto_play}
               }
               {if {modifier_mgr is_modifier_active mod_nohud}
                  {modifier_mgr toggle_modifier_enabled mod_nohud}
               }
               {if {modifier_mgr is_modifier_active mod_nopause}
                  {modifier_mgr toggle_modifier_enabled mod_nopause}
               }
               {if {modifier_mgr is_modifier_active mod_dx_no_overdrive}
                  {modifier_mgr toggle_modifier_enabled mod_dx_no_overdrive}
               }
               {overshell update_all}
               {ui goto_screen meta_loading_main_screen}
            }
         }
      }
   )
   (mod_auto_play
      {do
         {session disconnect}
         {session_mgr disconnect}
      }
      {unless {{$this get_session_mgr} is_online_enabled}
         {unless {modifier_mgr is_modifier_active mod_auto_play}
            {$this show_state kState_AutoPlayWarn}
         }
         {if {modifier_mgr is_modifier_active mod_auto_play}
            {do
               {modifier_mgr toggle_modifier_enabled $modifier}
               {if {modifier_mgr is_modifier_active mod_fakejuke}
                  {modifier_mgr toggle_modifier_enabled mod_fakejuke}
                  {modifier_mgr toggle_modifier_enabled mod_nohud}
                  {modifier_mgr toggle_modifier_enabled mod_nopause}
               }
               {overshell update_all}
               {ui goto_screen meta_loading_main_screen}
            }
         }
      }
   )
   (mod_auto_kick
      {do
         {session disconnect}
         {session_mgr disconnect}
         {modifier_mgr toggle_modifier_enabled $modifier}
         {if {$this in_game}
            {$this show_state kState_ModifierSongLoad}
         }
         {overshell update_all}
      }
   )
   (mod_chmode
      {unless {modifier_mgr is_modifier_active mod_chmode}
         {$this show_state kState_CHModeWarn}
      }
      {if {modifier_mgr is_modifier_active mod_chmode}
         {do
            {modifier_mgr toggle_modifier_enabled $modifier}
            {set $railscustomtexture TRUE}
            {if {== $rails none}
               {set $rails rb3}
            }
            {overshell update_all}
            {ui goto_screen meta_loading_main_screen}
         }
      }
   )
   (mod_whammyfx
      {modifier_mgr toggle_modifier_enabled $modifier}
      {if {$this in_game}
         {$this show_state kState_ModifierSongLoad}
      }
      {overshell update_all}
   )
   (mod_nomv
      {modifier_mgr toggle_modifier_enabled mod_nomv}
      {overshell update_all}
      {if_else {$this in_game}
         {$this show_state kState_ModifierSongLoad}
         {ui goto_screen meta_loading_main_screen}
      }
   )
   (mod_noblur
      {if_else {modifier_mgr is_modifier_active mod_noblur}
         {do 
            {set $blurisbackon TRUE}
            {modifier_mgr toggle_modifier_enabled mod_noblur}
         }
         {do
            {set $blurisbackon FALSE}
            {modifier_mgr toggle_modifier_enabled mod_noblur}
         }
      }
      {overshell update_all}
   )
   (mod_nocrust
      {if_else {modifier_mgr is_modifier_active mod_nocrust}
         {do 
            {set $crustisbackon TRUE}
            {modifier_mgr toggle_modifier_enabled mod_nocrust}
         }
         {do
            {set $crustisbackon FALSE}
            {modifier_mgr toggle_modifier_enabled mod_nocrust}
         }
      }
      {overshell update_all}
   )
   (mod_blackmenu
      {modifier_mgr toggle_modifier_enabled mod_blackmenu}
      {if_else {$this in_game}
         {$this show_state kState_ModifierSongLoad}
         {ui goto_screen meta_loading_main_screen}
      }
   )
   (mod_calibration
      {unless
         {modifier_mgr is_modifier_active mod_calibration}
         {$this show_state kState_CalibrationWarn}
      }
      {if
         {modifier_mgr is_modifier_active mod_calibration}
         {do
            {modifier_mgr toggle_modifier_enabled $modifier}
            {overshell update_all}
         }
      }
   )
)
#define DX_PART_SELECT_GUITAR
({choose_part.lst set_data (overshell_guitar overshell_bass overshell_keys)})
#define DX_PART_SELECT_DRUMS
(
   {do
      ($options
         {array 0})
      {if_else {== $dx_joypad_type_guitar kControllerDrum}
         {
            {do
               {push_back $options overshell_guitar}
               {push_back $options overshell_bass}
               {push_back $options overshell_keys}
            }
         }
            {if_else {== $dx_joypad_type_keys kControllerDrum}
               {
                  {do
                     {push_back $options overshell_keys}
                     {push_back $options overshell_real_keys}
                     {push_back $options overshell_guitar}
                     {push_back $options overshell_bass}
                     {push_back $options overshell_drums}
                  }
               }
               {
                  {do
                     {push_back $options overshell_drums}
                     {push_back $options overshell_drums_pro}
                  }
               }
            }
      }
      {choose_part.lst set_data $options}
   }
)
#define DX_PART_SELECT_VOCALS
(
   {do
      ($options
         {array 0})
      {push_back $options overshell_vocal_solo}
      {push_back $options overshell_vocal_harmony}
      {push_back $options overshell_guitar}
      {push_back $options overshell_bass}
      {push_back $options overshell_keys}
      {if {== $dx_joypad_type_keys kControllerVocals}
         {do {push_back $options overshell_real_keys}}}
      {push_back $options overshell_drums}
      {choose_part.lst set_data $options}
   }
)
#define DX_PART_SELECT_KEYS
({choose_part.lst set_data (overshell_keys overshell_real_keys overshell_guitar overshell_bass overshell_drums)})
#define DX_PART_SELECT_REAL_GUITAR
({choose_part.lst set_data (overshell_real_guitar overshell_real_bass overshell_guitar overshell_bass)})
#define DX_PART_SELECT_AUTOPLAY
({choose_part.lst set_data (overshell_vocal_solo overshell_vocal_harmony overshell_guitar overshell_bass overshell_drums overshell_drums_pro overshell_keys overshell_real_keys overshell_real_guitar overshell_real_bass)})
#define DX_PART_SELECT_HANDLERS
(
   {if_else {modifier_mgr is_modifier_active mod_auto_play}
      ;makes every instrument selectable when autoplay is on
      DX_PART_SELECT_AUTOPLAY
      ;otherwise set instrument selection normally
      {do
         {switch {{$this get_user} get_controller_type}
            (kControllerGuitar DX_PART_SELECT_GUITAR)
            (kControllerDrum DX_PART_SELECT_DRUMS)
            (kControllerVocals DX_PART_SELECT_VOCALS)
            (kControllerKeys DX_PART_SELECT_KEYS)
            (kControllerRealGuitar DX_PART_SELECT_REAL_GUITAR)
            {fail "No choose part view for this controller type"}
         }
      }
   }
   ;Selects default instrument by slot number
   {switch
      {$this get_slot_num}
      (0 {choose_part.lst set_selected $default_slot0})
      (1 {choose_part.lst set_selected $default_slot1})
      (2 {choose_part.lst set_selected $default_slot2})
      (3 {choose_part.lst set_selected $default_slot3})
      {fail {choose_part.lst set_selected $default_slot0}}
   }
)