#include ../../dx/ui/loading/dx_loading_tips.dta
#include ../../dx/ui/loading/dx_loading_handles.dta
{new
   BandPreloadPanel
   preload_panel
   (song_mgr song_mgr)
   (current_song
      {meta_performer song})
   DX_LOADING_HANDLES
   (enter
      {$this dx_loading_enter}
      {$this dx_load_tip_and_author_finder}
      {if $dx_prep_show_flow
         {set $dx_prep_show_flow FALSE}
      }
      {set $current_solo_highscore FALSE}
      {foreach $entry $highscore_tracker
         {if {== {elem $entry 0} {meta_performer song}}
            {set $current_solo_highscore {elem $entry 1}}
         }
      }
   )
   (preload_files
      ({song_mgr
            midi_file
            {meta_performer song}}
         FALSE
         #ifdef HX_PS3
         {song_mgr
            package_name
            {meta_performer song}}
         #endif)
      ({song_mgr
            song_file_path
            {meta_performer song}
            ".milo"}
         TRUE
         #ifdef HX_PS3
         ""
         #endif)
      ({song_mgr
            upgrade_midi_file
            {meta_performer song}}
         {!
            {song_mgr
               has_upgrade
               {meta_performer song}}}
         #ifdef HX_PS3
         {song_mgr
            upgrade_package_name
            {meta_performer song}}
         #endif))
   #ifdef HX_WII
   (load
      {content_mgr set_passive_errors_enabled FALSE})
   (finish_load
      {content_mgr set_passive_errors_enabled TRUE})
   #endif
   (on_preload_ok
      {net_sync disable}
      ;HEY BUD LISTEN UP
      ;I dont know why, i dont know how, but this fixes the rpcs3 crash when going to practice
      ;trying to check the gamemode makes it not work
      ;lowering the delay makes it not work
      ;ive tried several things
      ;its the L everyone has to take
      {thread_task ;delay going to practice to let the game catch up
         kTaskUISeconds
         (delay $dx_delay)
         (script
            {if_else
               {==
                  {ui current_screen}
                  preloading_screen}
               #ifdef HX_WII
               {ui goto_screen pregame_screen}
               #else
               {ui
                  goto_screen
                  {gamemode get game_screen}}
               #endif
               {ui goto_screen load_nextsong_screen}})
      }
   )
   (on_preload_failed
      {net_sync disable}
      #ifdef HX_WII
      {if_else
         {==
            {ui current_screen}
            preloading_screen}
         {ui goto_screen wii_preloading_failed_screen}
         {ui goto_screen wii_preload_failed_nextsong_screen}}
      #else
      {passive_messenger trigger_skip_song_msg}
      {if_else
         {!
            {meta_performer is_last_song}}
         {if_else
            {==
               {ui current_screen}
               preloading_screen}
            {ui goto_screen preloading_failed_screen}
            {ui goto_screen preload_failed_nextsong_screen}}
         {ui goto_screen meta_loading_continue_screen}}
         #endif)}
{new
   BandScreen
   preloading_screen
   (panels meta preload_panel #ifndef HX_WII transition_fallback_panel #endif)
   (focus preload_panel)
   #ifdef HX_XBOX
   (show_fallback $dx_xbox_show_fallback)
   #else
   (show_fallback TRUE)
   #endif
   (enter
      #ifndef HX_WII
      {transition_fallback_panel
         set_showing
         [show_fallback]}
      {{transition_fallback_panel find waiting.lbl}
         set_showing
         FALSE}
      {if
         [show_fallback]
         {transition_fallback_panel start_loading_quick_anim}}
      ;{$this set show_fallback FALSE}
      #endif
      {session_mgr set_active_roster TRUE}
      {overshell set_active_status kOvershellInactive}
      {seed_random_context
         {session_mgr get_net_random_seed}}
      {game set multi_event FALSE}
      {song_mgr
         add_recent_song
         {meta_performer song}}
      {preload_panel dx_load_tip_and_author_finder}
      {preload_panel dx_set_up_loading_tip_label}
      {if
         {exists game}
         {acc_mgr clear_goal_info}})
   (TRANSITION_COMPLETE_MSG
      {net_sync disable}
      #ifdef HX_WII {meta_performer load_festival} #endif
      kDataUnhandled)}
{new
   BandScreen
   preloading_failed_screen
   (panels meta)
   (TRANSITION_COMPLETE_MSG
      {meta_performer skip_song}
      {ui goto_screen preloading_screen})}
#ifdef HX_WII
#define WII_PRELOADING_FAILED_HANDLERS
((focus dialog_panel)
   (error_msg '')
   (TRANSITION_COMPLETE_MSG
      {do
         ($data
            {song_mgr
               song_name
               {meta_performer song}})
         ($error
            {content_mgr get_last_result})
         {switch
            $error
            (kOpMissingCard
               {set $error_msg dlc_restore_sdcardremoved})
            (kOpCorruptData
               {set
                  $error_msg
                  (dlc_restore_corrupt_name $data)})
            (kOpCorruptCard
               {set
                  $error_msg
                  (dlc_restore_corrupt_name $data)})
            (kOpOutOfDate
               {set
                  $error_msg
                  (dlc_restore_outofdate $data $data)})
            (kOpNoLicense
               {set
                  $error_msg
                  (dlc_restore_nolicense $data $data)})
            (kOpIncompatibleCard
               {set $error_msg dlc_restore_incompatible})
            (kOpCorruptContent
               {set $error_msg dlc_restore_corrupt_content})
            (kOpFail
               {set
                  $error_msg
                  (dlc_restore_failed_name $data)})
            {if_else
               {!
                  {session_mgr is_local}}
               {set
                  $error_msg
                  (wii_error_generic)}
               {set
                  $error_msg
                  (dlc_restore_failed_name $data)}}}
         {dialog_panel set_ok $error_msg}}))
{new
   BandScreen
   wii_preloading_failed_screen
   (panels meta dialog_panel)
   WII_PRELOADING_FAILED_HANDLERS
   (SELECT_MSG
      {ui goto_screen meta_loading_continue_screen})}
#endif