{new
   BandPreloadPanel
   preload_panel
   (song_mgr song_mgr)
   (current_song
      {meta_performer song})
   (enter
      #ifndef HX_WII
      {if $dx_author_finder
         {if
            {||
               {gamemode in_mode defaults} {gamemode in_mode qp_party_shuffle}
               {gamemode in_mode qp_coop} {gamemode in_mode campaign}
               {gamemode in_mode party_shuffle} {gamemode in_mode tour}
               {gamemode in_mode practice} {gamemode in_mode qp_practice}
            }
            MTV_DTA_PREPPER
         }
      }
      #endif ; dx - load prepper unless on ps3 hw
   )
   (preload_files
      ({song_mgr
            midi_file
            {meta_performer song}}
         FALSE
         #ifdef HX_PS3
         {song_mgr
            package_name
            {meta_performer song}}
         #endif)
      ({song_mgr
            song_file_path
            {meta_performer song}
            ".milo"}
         TRUE
         #ifdef HX_PS3
         ""
         #endif)
      ({song_mgr
            upgrade_midi_file
            {meta_performer song}}
         {!
            {song_mgr
               has_upgrade
               {meta_performer song}}}
         #ifdef HX_PS3
         {song_mgr
            upgrade_package_name
            {meta_performer song}}
         #endif))
   #ifdef HX_WII
   (load
      {content_mgr set_passive_errors_enabled FALSE})
   (finish_load
      {content_mgr set_passive_errors_enabled TRUE})
   #endif
   (on_preload_ok
      {net_sync disable}
      {if_else
         {==
            {ui current_screen}
            preloading_screen}
         {ui
            goto_screen
            #ifdef HX_WII
            pregame_screen
            #else
            {gamemode get game_screen}
            #endif}
         {ui goto_screen load_nextsong_screen}})
   (on_preload_failed
      {net_sync disable}
      #ifdef HX_WII
      {if_else
         {==
            {ui current_screen}
            preloading_screen}
         {ui goto_screen wii_preloading_failed_screen}
         {ui goto_screen wii_preload_failed_nextsong_screen}}
      #else
      {passive_messenger trigger_skip_song_msg}
      {if_else
         {!
            {meta_performer is_last_song}}
         {if_else
            {==
               {ui current_screen}
               preloading_screen}
            {ui goto_screen preloading_failed_screen}
            {ui goto_screen preload_failed_nextsong_screen}}
         {ui goto_screen meta_loading_continue_screen}}
         #endif)}
{new
   BandScreen
   preloading_screen
   (panels meta preload_panel #ifndef HX_WII transition_fallback_panel #endif)
   (focus preload_panel)
   #ifndef HX_WII (show_fallback FALSE) #endif
   (enter
      #ifndef HX_WII
      {transition_fallback_panel
         set_showing
         [show_fallback]}
      {{transition_fallback_panel find waiting.lbl}
         set_showing
         FALSE}
      {if
         [show_fallback]
         {transition_fallback_panel start_loading_quick_anim}}
      {$this set show_fallback FALSE}
      #endif
      {session_mgr set_active_roster TRUE}
      {overshell set_active_status kOvershellInactive}
      {seed_random_context
         {session_mgr get_net_random_seed}}
      {game set multi_event FALSE}
      {song_mgr
         add_recent_song
         {meta_performer song}}
      {if
         {exists game}
         {acc_mgr clear_goal_info}})
   (TRANSITION_COMPLETE_MSG
      {net_sync disable}
      #ifdef HX_WII {meta_performer load_festival} #endif
      kDataUnhandled)}
{new
   BandScreen
   preloading_failed_screen
   (panels meta)
   (TRANSITION_COMPLETE_MSG
      {meta_performer skip_song}
      {ui goto_screen preloading_screen})}
#ifdef HX_WII
#define WII_PRELOADING_FAILED_HANDLERS
((focus dialog_panel)
   (error_msg '')
   (TRANSITION_COMPLETE_MSG
      {do
         ($data
            {song_mgr
               song_name
               {meta_performer song}})
         ($error
            {content_mgr get_last_result})
         {switch
            $error
            (kOpMissingCard
               {set $error_msg dlc_restore_sdcardremoved})
            (kOpCorruptData
               {set
                  $error_msg
                  (dlc_restore_corrupt_name $data)})
            (kOpCorruptCard
               {set
                  $error_msg
                  (dlc_restore_corrupt_name $data)})
            (kOpOutOfDate
               {set
                  $error_msg
                  (dlc_restore_outofdate $data $data)})
            (kOpNoLicense
               {set
                  $error_msg
                  (dlc_restore_nolicense $data $data)})
            (kOpIncompatibleCard
               {set $error_msg dlc_restore_incompatible})
            (kOpCorruptContent
               {set $error_msg dlc_restore_corrupt_content})
            (kOpFail
               {set
                  $error_msg
                  (dlc_restore_failed_name $data)})
            {if_else
               {!
                  {session_mgr is_local}}
               {set
                  $error_msg
                  (wii_error_generic)}
               {set
                  $error_msg
                  (dlc_restore_failed_name $data)}}}
         {dialog_panel set_ok $error_msg}}))
{new
   BandScreen
   wii_preloading_failed_screen
   (panels meta dialog_panel)
   WII_PRELOADING_FAILED_HANDLERS
   (SELECT_MSG
      {ui goto_screen meta_loading_continue_screen})}
#endif