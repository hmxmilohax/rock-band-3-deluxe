{new
   BandPreloadPanel
   preload_panel
   (song_mgr song_mgr)
   (current_song
      {meta_performer song})
   (enter
      {dx_countdown_var_reset}
      {dx_midi_parser_var_reset}
      {if_else {! {session_mgr is_local}}
         {set $dx_delay 2}
         {if_else $dx_song_delay
            {set $dx_delay 2}
            {set $dx_delay 0}
         }
      }
      {set $speedmod_top $leaderspeed}
   )
   (preload_files
      ({song_mgr
            midi_file
            {meta_performer song}}
         FALSE
         #ifdef HX_PS3
         {song_mgr
            package_name
            {meta_performer song}}
         #endif)
      ({song_mgr
            song_file_path
            {meta_performer song}
            ".milo"}
         TRUE
         #ifdef HX_PS3
         ""
         #endif)
      ({song_mgr
            upgrade_midi_file
            {meta_performer song}}
         {!
            {song_mgr
               has_upgrade
               {meta_performer song}}}
         #ifdef HX_PS3
         {song_mgr
            upgrade_package_name
            {meta_performer song}}
         #endif))
   (on_preload_ok
      {net_sync disable}
      ;HEY BUD LISTEN UP
      ;I dont know why, i dont know how, but this fixes the rpcs3 crash when going to practice
      ;trying to check the gamemode makes it not work
      ;lowering the delay makes it not work
      ;ive tried several things
      ;its the L everyone has to take
      {thread_task ;delay going to practice to let the game catch up
         kTaskUISeconds
         (delay $dx_delay)
         (script
            {if_else
               {==
                  {ui current_screen}
                  preloading_screen}
               {ui
                  goto_screen
                  {gamemode get game_screen}}
               {ui goto_screen load_nextsong_screen}})
      }
   )
   (on_preload_failed
      {net_sync disable}
      {passive_messenger trigger_skip_song_msg}
      {if_else
         {!
            {meta_performer is_last_song}}
         {if_else
            {==
               {ui current_screen}
               preloading_screen}
            {ui goto_screen preloading_failed_screen}
            {ui goto_screen preload_failed_nextsong_screen}}
         {ui goto_screen meta_loading_continue_screen}})}
{new
   BandScreen
   preloading_screen
   (panels meta preload_panel transition_fallback_panel)
   (focus preload_panel)
   (show_fallback FALSE)
   (enter
   	DX_SET_LOADING_TIP ; dx - set a random loading tip and apply it to the loading label
      {$this set show_fallback TRUE}
      {transition_fallback_panel
         set_showing
         [show_fallback]}
      {{transition_fallback_panel find waiting.lbl}
         set_showing
         FALSE}
      {if
         [show_fallback]
         {transition_fallback_panel start_loading_quick_anim}}
      {$this set show_fallback FALSE}
      {session_mgr set_active_roster TRUE}
      {overshell set_active_status kOvershellInactive}
      {seed_random_context
         {session_mgr get_net_random_seed}}
      {game set multi_event FALSE}
      {song_mgr
         add_recent_song
         {meta_performer song}}
      {if $dx_author_finder_is_enabled
         {unless {gamemode in_mode trainer}
            {dx_author_finder_var_reset}
            MTV_DTA_PREPPER
         }
         {unless $dx_mtv_dta_reader_once
            {set $dx_mtv_dta_reader_once TRUE} ;no matter how many times this function gets called, we only need to read these dtas once because its very heavy
            MTV_DTA_READER
         }
         {if $dx_loading_tip_var
            {{transition_fallback_panel find loading.lbl} set_token_fmt {sprint $dx_loading_tip_var}} ; set the loading tip found in songs.dta
         }
      }
      {unless $dx_loading_tip_var
         {{transition_fallback_panel find loading.lbl} set text_token {localize {random_elem {elem DX_LOADING_TIPS $index}}}} ; set a random loading tip
      }
      {set $dx_loading_tip_var FALSE}
      {if
         {exists game}
         {acc_mgr clear_goal_info}})
   (TRANSITION_COMPLETE_MSG
      {net_sync disable}
      kDataUnhandled)}
{new
   BandScreen
   preloading_failed_screen
   (panels meta)
   (TRANSITION_COMPLETE_MSG
      {meta_performer skip_song}
      {ui goto_screen preloading_screen})}