(RndDir
   (types
      (mtv_overlay
         (editor
            (show
               script
               (script
                  {if
                     {exists fade.anim}
                     {fade.anim set_frame 50}}))
            (hide
               script
               (script
                  {if
                     {exists fade.anim}
                     {fade.anim set_frame 0}}))
            (play
               script
               (script
                  {if
                     {exists fade.anim}
                     {fade.anim animate}})))
         (enter
            {$this dx_set_mtv_pos} ;set the custom position of the mtv_overlay on enter
            {set $dx_mtvup TRUE} ;some code crashes when ran and mtv is not available
            {if
               {exists game}
               {game
                  add_sink
                  $this
                  (intro_end)}
               {unless {gamemode in_mode trainer}
                  {$this setup_text}
               }
               {if
                  {exists fade.anim}
                  {fade.anim set_frame {if_else {== $dx_song_title always} 50 0}} ; dx - if song title always is enabled set the current frame of the fade animation to fully visible
               }
            }
         )
         DX_MTV_SET_POS_HANDLER
         DX_MTV_FORMATTER_HANDLER
         MTV_MOD_WRITER_HANDLER
         (exit
            {if
               {exists game}
               {game remove_sink $this}})
         (intro_end
            {if
               {&&
                  {exists game}
                  {gamemode get show_mtv_overlay}
                  {exists fade.anim}}
               {if_else
                  {|| $dx_long_mtv_delay {gamemode is_tour}}
                  {script_task
                     kTaskSeconds
                     (delay 7.0)
                     (script
                        {fade.anim animate})
                  }
                  {switch $dx_song_title
                     (always {fade.anim set_frame 50})
                     (never {fade.anim set_frame 0})
                     (default {fade.anim animate})
                     (delayed {fade.anim animate})
                  }
               }
            }
         )
         (setup_text
            {do
               ($short_name
                  {meta_performer song})
               ($song_title
                  {{song_mgr get_meta_data {meta_performer song}} title}
               )
               #ifdef HX_WII
               MTV_WRITER_NO_AUTHOR_FINDER
               #else
               ;{if_else $dx_ps3hw
               ;   MTV_WRITER_NO_AUTHOR_FINDER
                  MTV_WRITER_CHOOSER ; dx - this is where we run mtv overlay setup
               ;}
               #endif
            }
         )
      )
   )
)
